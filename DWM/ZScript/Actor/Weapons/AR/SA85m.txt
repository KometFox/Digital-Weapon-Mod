class RMD_SA85m : BaseWeapon
{
	double ScopeZoom;

	Default
	{
		Weapon.SlotNumber 5;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType1 "RMD_MediumBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PISA85m";
		Obituary "$OB_MPCHAINGUN";
		Tag "SA 85-m";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 50;
		BaseWeapon.Magazine 50;	
	}
	
	Action state ReloadCheck()
	{
		return B_ReloadGoTo(
							"ReadyReal", 
							"IronReady", 
							"IronSightDeselect", 
							"ReloadNow", 
							Invoker.AmmoType1, 
							Invoker.Magazine,
							Invoker.MagazineMax, 
							1);
	}
	
	//Blam blam
	Action Void FireBullets()
	{
		//Check for Ammo
		if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_ACTIVE)
		{
			SetWeaponState("IronReady");
			Return;
		}
		else if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_INACTIVE)
		{
			SetWeaponState("ReadyReal");
			Return;
		}		
	
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
			

			
		if (Invoker.IronSight == IRON_INACTIVE)
		{	
			A_FireProjectile("RMD_SA85m_Bullet",frandom(-0.75, 0.75), 1, 6, 4, CMF_AIMDIRECTION,frandom(-0.75, 0.75));
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (13, 2.5, -2.0));	
			B_SpawnCasing("RifleCase", (-30, 7, 1.5), 180);
		}
		else if (Invoker.IronSight == IRON_ACTIVE)
		{
			A_FireProjectile("RMD_SA85m_Bullet",frandom(-0.15, 0.15), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.15, 0.15));
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (10, -0.3, -1.0));
			B_SpawnCasing("RifleCase", (-25, 2, -4), 180);
		}

		A_AlertMonsters();
		A_StartSound("SA85m/Fire", CHAN_WEAPON);
	}


	States
	{

	Ready:
		CHGG A 0;
		IDLE A 0 
		{
			A_Jump(256, "Init");
		}
		Goto Init;
	
	Init:
		IDLE A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOAR");
				RMD_BARINFO.SetAmmo1("RMD_MediumBullets");
				A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
		}
		Goto ReadyReal;
	
	ReadyReal:
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	//Ironsight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	Deselect:
		SELC HGFEDCBA 1;
		SELC A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Loop;
		
	Select:
		TNT1 A 0 
		{
				A_StartSound("AssaultRifle/Select", CHAN_WEAPON +5);
				A_TakeInventory("Reloading", 99);
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOAR");
				RMD_BARINFO.SetAmmo1("RMD_MediumBullets");
		}
		SELC ABCDEFGH 1;
		SELC H 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
			


	//Ironsight Mode
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(2.25);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		TNT1 A 0
		{
			Invoker.IronSight = IRON_INACTIVE;
			
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "ReloadNow");
			}

			Return A_Jump(256, "ReadyReal");			
		}
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto ReadyReal;

	Fire:
		TNT1 A 0
		{
			if (Invoker.IronSight == IRON_ACTIVE)
			{
				Return A_Jump(256, "IronFireNow");
			}
			else
			{
				Return A_Jump(256, "FireNow");				
			}

			Return A_Jump(256, "ReadyReal");		
		}
		Goto ReadyReal;

	//Bullets
	FireNow:
		FIR1 A 1 FireBullets();
		FIR1 BCD 1;
		FIR1 D 0 A_ReFire("FireNow");
		FIR1 EF 1;
		Goto ReadyReal;
	
	IronFireNow:
		IFI1 A 1 FireBullets();
		IFI1 ABC 1;
		IFI1 C 0 A_ReFire("IronFireNow");
		IFI1 EF 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
		
	//The Magazine
	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 99);
		REL1 ABCDEF 2;
		TNT1 A 0
		{
			A_StartSound("SA85m/MagOut", CHAN_WEAPON +1);
			A_FireProjectile("EmptyMagSpawn",-5,0,8,-4);
		}
		REL1 JKLM 2;
		TNT1 A 0		
		{
			A_StartSound("SA85m/MagIn", CHAN_WEAPON +2);
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
			RMD_BARINFO.Set_Mag(invoker.Magazine);
		}
		REL1 NOPQRS 2;
		TNT1 A 0 
		{
			A_StartSound("SA85m/Cocking", CHAN_WEAPON +2);
		}
		REL1 TUVWXYZ 2;
		REL2 AB 2;
		Goto ReadyReal;

	//FuckingMother
	Template:
		IRND A 0;
		IFIN A 0;
		Goto Ready;
	}
}

Class RMD_SA85m_Bullet : RMD_BaseBullet
{
	Default
	{		
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 17; 
		RMD_BaseBullet.DamageRand 1.8;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 240;		
		
		Scale 0.75;
		Decal "RMD_AR_BulletHoles";
	}
}



