class RMD_M16A3 : BaseWeapon
{
	double ScopeZoom;

	Default
	{
		Weapon.SlotNumber 5;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType1 "RMD_ARBullets";
		Weapon.AmmoType2 "RMD_Grenades";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIM16A3";
		Obituary "$OB_MPCHAINGUN";
		Tag "M16A3 (a)";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 70;
		BaseWeapon.Magazine 70;	
	}
	
	Action state ReloadCheck()
	{
		return B_ReloadGoTo(
							"ReadyReal", 
							"IronReady", 
							"IronSightDeselect", 
							"ReloadNow", 
							Invoker.AmmoType1, 
							Invoker.MagazineMax, 
							1);
	}
	
	//Blam blam
	Action Void FireBullets()
	{
		//Check for Ammo
		if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_ACTIVE)
		{
			SetWeaponState("IronReady");
			Return;
		}
		else if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_INACTIVE)
		{
			SetWeaponState("ReadyReal");
			Return;
		}		
	
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
			

			
		if (Invoker.IronSight == IRON_INACTIVE)
		{	
			A_FireProjectile("RMD_M16A3_Bullet",frandom(-0.75, 0.75), 1, 6, 4, CMF_AIMDIRECTION,frandom(-0.75, 0.75));
			//A_FireBullets(0.7, 0.7, 1, 11 * frandom(1.0, 2.0), "URPG_RicochetEffect", BULLETFIRE, 0, null);
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (15, 3.0, -2.0));	
			B_SpawnCasing("RifleCase", (12, -15.0, -1.0));
		}
		else if (Invoker.IronSight == IRON_ACTIVE)
		{
			A_FireProjectile("RMD_M16A3_Bullet",frandom(-0.15, 0.15), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.15, 0.15));
			//A_FireBullets(0.7, 0.7, 1, 11 * frandom(1.0, 2.0), "URPG_RicochetEffect", BULLETFIRE, 0, null);
			
			B_SpawnCasing("RifleCase", (12, 16.0, -0.5));
		}

		A_AlertMonsters();
		A_StartSound("M16A3/Fire", CHAN_WEAPON);
	}


	States
	{

	Ready:
		CHGG A 0;
		IDLE A 0 
		{
			A_Jump(256, "Init");
		}
		Goto Init;
	
	Init:
		IDLE A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOAR");
				RMD_BARINFO.SetAmmo1("RMD_ARBullets");
				A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
		}
		Goto ReadyReal;
	
	ReadyReal:
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	//Ironsight Mode
	IronReady:
		SCOA A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	Deselect:
		SELC DCBA 1 A_Lower;
		SELC A 1 A_Lower;
		TNT1 A 0 A_Lower(100);
		Loop;
		
	Select:
		TNT1 A 0 
		{
				A_StartSound("AssaultRifle/Select", CHAN_WEAPON +5);
				A_TakeInventory("Reloading", 99);
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOAR");
				RMD_BARINFO.SetAmmo1("RMD_ARBullets");
		}
		SELC ABCD 1 A_Raise;
		IDLE A 1 A_Raise(100);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronSightDeselect:
		IDLE A 0 
		{
			A_SetCrosshair(0);
			A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
		}
		IDLE A 7;
		IDLE A 0
		{
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "ReloadNow");
			}

			Return A_Jump(256, "ReadyReal");			
		}
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto ReadyReal;
		
	AltHold:
		SCOA A 1
		{
			if ( Invoker.ScopeZoom <= 8.0 )
			{
				A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
				Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
			}
		}
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;
	
	Fire:
		TNT1 A 0
		{
			if (Invoker.IronSight == IRON_ACTIVE)
			{
				Return A_Jump(256, "IronFireNow");
			}
			else
			{
				Return A_Jump(256, "FireNow");				
			}

			Return A_Jump(256, "ReadyReal");		
		}
		Goto ReadyReal;

	//Bullets
	FireNow:
		FIRE A 1 FireBullets();
		FIRE BCD 1;
		FIRE A 0 A_ReFire("FireNow");
		FIRE EF 1;
		Goto ReadyReal;
	
	IronFireNow:
		SCOA A 1 FireBullets();
		SCOA AAA 1;
		SCOA A 0 A_ReFire("IronFireNow");
		SCOA AA 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
		
	//The Magazine
	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 99);
		RELO ABCDEF 2;
		TNT1 A 0
		{
			A_StartSound("M16A3/MagOut", CHAN_WEAPON +1);
			A_FireProjectile("EmptyMagSpawn",-5,0,8,-4);
		}
		RELO GHIJKL 3;
		TNT1 A 0
		{
			A_StartSound("M16A3/MagIn", CHAN_WEAPON +2);
			
			ReloadMagazine(PRIMARY, Invoker.MagazineMax, Invoker.MagazineMax);				
			RMD_BARINFO.Set_Mag(invoker.Magazine);
	
			Return A_Jump(0, "ReadyReal");	
		}
		RELO MNOPQS 2;		
		TNT1 A 0 A_StartSound("M25/Cocking", CHAN_WEAPON +3);
		RELO STUVWX 2;
		Goto ReadyReal;

	//FuckingMother
	Template:
		IRND A 0;
		IFIN A 0;
		Goto Ready;
	}
}

Class RMD_M16A3_Bullet : RMD_BaseBullet
{
	Default
	{		
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 8; 
		RMD_BaseBullet.DamageRand 2.85;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 240;		
		
		Scale 0.75;
		Decal "RMD_AR_BulletHoles";
	}
}



