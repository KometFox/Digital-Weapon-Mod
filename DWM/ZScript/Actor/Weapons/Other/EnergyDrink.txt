class RMD_EnergyDrink : BaseWeapon
{
	enum DRINKY
	{
		CLOSED = 0,
		OPEN = 1,
	}
	
	int OpenedUp;
	

	Default
	{
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 0;
		Weapon.AmmoUse 0;
		Weapon.AmmoType "RMD_HealthAmmo";
		Weapon.AmmoGive 0;
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIENGCAN";
		Obituary "$OB_MPCHAINGUN";
		Tag "Energy Drink";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 100;
		BaseWeapon.Magazine 0;
		
	}
	
	Action state CondCheck1(statelabel JumpTo1, statelabel JumpTo2)
	{
		if (Invoker.Magazine > 0 && Invoker.OpenedUp == OPEN && CheckPlayerHealth())
		{	
			Return ResolveState(JumpTo1);				
		}
		else if (Invoker.Magazine > 0 && Invoker.OpenedUp == CLOSED) 
		{
			Return ResolveState(JumpTo2);
		}		
	
		Return ResolveState("ReadyReal");	
	}
	
	Action state CondCheck2(statelabel JumpTo1)
	{
		if (Invoker.Magazine > 0 && CheckPlayerHealth())
			Return ResolveState(JumpTo1);				
	
		Return ResolveState("HoldStop");	
	}

	Action bool CheckPlayerHealth()
	{
		if (Player)
		{
			PlayerPawn CPlayer = Player.mo;
		
			if (CPlayer.Health < 100)
				return true;
		}
			
		return false;
	}

	Action void DrinkUp()
	{		
		if (Invoker.Magazine > 0)
		{
			A_GiveInventory("RMD_EnergyDrink_DrinkUp", 1);
			Invoker.Magazine -= 3;
			Invoker.Magazine = Clamp(Invoker.Magazine, 0, 100);
			RMD_BARINFO.Set_Mag(invoker.Magazine);
		}
		
	}
	
	Action void SoundEffect()
	{
		A_StartSound("Items/Drink", CHAN_WEAPON);
	}
	
	Action State CheckOpen()
	{
		if (Invoker.OpenedUp == OPEN)
		{
			return ResolveState("ReadyOpen");
		}
		else
		{
			return ResolveState("ReadyReal");
		}	
		
		return ResolveState("ReadyReal");
	}

	States
	{

	Ready:
		CHGG A 0;
		TNT1 A 0 
		{
			A_Jump(256, "Init");
		
		}
	
	Init:
		TNT1 A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				Invoker.OpenedUp = CLOSED;
				RMD_BARINFO.SetAmmoIcon("AMMOHP");
				RMD_BARINFO.SetAmmo1("RMD_HealthAmmo");
		}
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
	
	ReadyOpen:
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		OPEN Q 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;		

	Deselect:
		SELC FEDCBA 1 A_Lower;
		SELC A 1 A_Lower;
		TNT1 A 0 A_Lower(100);
		Loop;
		
	Select:
		TNT1 A 0 
		{
				//A_StartSound("Pistol/Select", CHAN_WEAPON +5);
				A_TakeInventory("Reloading", 99);
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOHP");
				RMD_BARINFO.SetAmmo1("RMD_HealthAmmo");
		}
		SELC ABCDEF 1 A_Raise;
		SELC F 1 A_Raise(100);
        Goto ReadyReal;
		
	Fire:
		TNT1 A 0 CondCheck1("DrinkUp", "OpenUp");
		Goto ReadyReal;
	

	OpenUp:
		OPEN ABCDEFGHI 2;
		TNT1 A 0 
		{
			A_StartSound("Beer/Open");
			Invoker.OpenedUp = OPEN;
		}
		OPEN JKLMNOPQ 2;
		Goto ReadyOpen;
	
	DrinkUp:
		TNT1 A 0 
		{		
			if (Invoker.Magazine <= 0 || CheckInventory("NOAMMO", 1) == 1)
			{
				A_GiveInventory("NOAMMO", 1);
				return CheckOpen();
			}
			
			return ResolveState("DrinkUp") +1;
		}	
		DRNK ABCDEFG 2;
		TNT1 A 0 A_StartSound("Beer/Drink", CHAN_WEAPON, CHANF_LOOP);
		Goto DrinkLoop;
	
	DrinkLoopCheck:
		TNT1 A 0 CondCheck2("DrinkLoop");
		Goto HoldStop;
	
	DrinkLoop:
		DRNK G 1 DrinkUp();
		TNT1 A 0 A_ReFire("DrinkLoopCheck");
		Goto HoldStop;
	HoldStop:
		DRNK GFEDCBA 2;
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		TNT1 A 0 CheckOpen();
		Goto ReadyReal;
		
	Throw:
		THRW ABCD 2;
		Goto Init;		
	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
	
	
	ReloadNow:
		THRW ABCD 2;
		TNT1 A 0
		{
			Actor ProjP1, ProjP2;
			[ProjP1, ProjP2] = A_FireProjectile("EmptyEnergyCan1", 0, 0, -6, 8, BULLETFIRE);
		
			if (ProjP2)
			{
				ProjP2.Speed = 15;			
			}
		
		}
		SELC ABCDEF 2;
		TNT1 A 0
		{
			A_TakeInventory("GoToReload", 1);
			A_TakeInventory("NOAMMO");
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
			Invoker.OpenedUp = CLOSED;
			RMD_BARINFO.Set_Mag(invoker.Magazine);			
	
			Return A_Jump(0, "ReadyReal");	
		}
		Goto ReadyReal;
	}
}

class RMD_EnergyDrink_DrinkUp : Health
{
	Default
	{
		-COUNTITEM
		Inventory.Amount 3;
		Inventory.MaxAmount 100;
		Inventory.PickupMessage "";
		Inventory.PickupSound "";
	}
	
	States
	{

		Spawn:
			IDLE A 0;
			Stop;
	}	
}

