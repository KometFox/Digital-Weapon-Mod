class MP5 : BaseWeapon
{
	const Primary_AmmoU = 2;
	const STAB = 50; //Stability

	Default
	{
		Weapon.SelectionOrder 1;
		Weapon.SlotNumber 3;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_PistolBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIMP5";
		Obituary "$OB_MPCHAINGUN";
		Tag "MP5";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 70;
		BaseWeapon.Magazine 70;
		
	}

	Action void FireBullets()
	{ 
		//Check for Ammo
		if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_ACTIVE)
		{
			SetWeaponState("IronReady");
			Return;
		}
		else if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_INACTIVE)
		{
			SetWeaponState("ReadyReal");
			Return;
		}		
	
		if (invoker.IronSight == IRON_INACTIVE)
		{

			A_FireProjectile("RMD_MP5_Projectile",frandom(-0.5, 0.5), 1, 6, 4, CMF_AIMDIRECTION, frandom(-0.5, 0.5));
			//A_FireBullets(0.8, 0.8, 1, 7 * Frandom(1.0, 1.8), "URPG_RicochetEffect", BULLETFIRE, 0, null);
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (13, 2.7, -2.0));	
			B_SpawnCasing("PistolCase", (12, -15.0, 3.0));
		}
			
		else
		{
			A_FireProjectile("RMD_MP5_Projectile",frandom(-0.25, 0.25), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.25, 0.25));
			//A_FireBullets(0.6, 0.6, 1, 7 * Frandom(1.0, 1.8), "URPG_RicochetEffect", BULLETFIRE, 0, null);

			B_MuzzleFlash("RMD_BulletMuzzleFlash", (13, 0.0, -1.2));	
			B_SpawnCasing("PistolCase", (12, 0.0, -2.0));
		}

		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
				
		A_StartSound("MP5/Fire", CHAN_WEAPON);
		A_AlertMonsters();
	}

	States
	{
		
	Ready:
		CHGG A 0;
		TNT1 A 0 
		{
			A_Jump(256, "Init");
		
		}
	
	Init:
		TNT1 A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOSMG");
				RMD_BARINFO.SetAmmo1("RMD_PistolBullets");
		}
		Goto ReadyReal;
		
	ReadyReal:
		IDLE A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;

	Deselect:
		SEL1 GFEDCBA 1 A_Lower;
		SEL1 A 1 A_Lower;
		TNT1 A 0 A_Lower(100);
		Loop;
		
	Select:
		TNT1 A 0 
		{
				A_StartSound("SMG/Select", CHAN_WEAPON +5);
				A_TakeInventory("Reloading", 99);
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOSMG");
				RMD_BARINFO.SetAmmo1("RMD_PistolBullets");
		}
		SEL1 ABCDEFG 1 A_Raise;
		IDLE A 1 A_Raise(100);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
		
	IronSightAnim:
		IRO1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.5);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		IRO1 A 0
		{
			Invoker.IronSight = IRON_INACTIVE;
			
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "ReloadNow");
			}

			Return A_Jump(256, "ReadyReal");			
		}
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0
		{
			if (Invoker.IronSight == IRON_INACTIVE)
			{
				Invoker.IronSight = IRON_ACTIVE;
				
				Return A_Jump(256, "IronSightAnim");

			}
			else
			{
				Invoker.IronSight = IRON_INACTIVE;
				
				Return A_Jump(256, "IronSightDeselect");
			}	
		}
		Goto ReadyReal;
		
	Fire:
		TNT1 A 0
		{
		
			if (Invoker.MagLeft(Invoker.Magazine))
			{
				if (Invoker.IronSight == IRON_ACTIVE)
				{
					Return A_Jump(256, "IronFireNow");
				}
				else
				{	
					Return A_Jump(256, "FireNow");				
				}
			}
		
		
			//If it has no ammo left
			if (Invoker.IronSight == IRON_ACTIVE)
			{
				Return A_Jump(256, "IronReady");
			}
			
			else
			{
				Return A_Jump(256, "ReadyReal");				
			}		
			
		}
		Goto ReadyReal;
		
	FireNow:
		FIR1 A 1 FireBullets();
		FIR1 BC 1;
		IDLE A 0 A_ReFire;
		FIR1 DEFGH 1;
		Goto ReadyReal;
		
	IronFireNow:
		IFI1 A 1 FireBullets();
		IFI1 BC 1;
		IFI1 A 0 A_ReFire;
		IFI1 DEFGH 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		REL1 ABCDEFGHIJKLMNO 2;
		TNT1 A 0
		{
			A_StartSound("MP5/MagOut", CHAN_WEAPON);
			A_FireProjectile("SMGMagSpawn",-5,0,8,-4);
		}
		REL1 PQRSTUVWXYZ 2;
		REL2 AB 2;
		TNT1 A 0
		{
			A_StartSound("MP5/MagIn", CHAN_WEAPON +1);
			
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
			RMD_BARINFO.Set_Mag(invoker.Magazine);
	
			Return A_Jump(0, "ReadyReal");	
		}
		REL2 CDEFGHI 2;		
		TNT1 A 0 A_StartSound("MP5/Cocking", CHAN_WEAPON);
		REL2 JKLMNOPQRSTUVWXYZ 2;
		Goto ReadyReal;

	}
}


Class RMD_MP5_Projectile : RMD_BaseBullet
{

Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 11;
		RMD_BaseBullet.DamageRand 1.85;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 275;		
		
		Scale 0.75;
		Decal "RMD_SMG_BulletHoles";
	}
}