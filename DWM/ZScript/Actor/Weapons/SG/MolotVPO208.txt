class RMD_MolotVPO208 : BaseWeapon
{
	//Firing Accuracy
	Const FireAngle = 0.45;
	Const IronFireAngle = 0.25;

	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.SlotNumber 4;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Shells";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "Molot VPO-208";
		Inventory.AltHudIcon "PIMOL208";
		BaseWeapon.IronSight IRON_INACTIVE;

		BaseWeapon.MagazineMax 15;
		BaseWeapon.Magazine 15;
		
	}
	
	Action void FireBullets()
	{	
		//Check for Ammo
		if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_ACTIVE)
		{
			SetWeaponState("IronReady");
			Return;
		}
		else if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_INACTIVE)
		{
			SetWeaponState("ReadyReal");
			Return;
		}				
			
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			for (int i = 0; i < 5; i++)
			{
				A_FireProjectile("RMD_MolotVPO208Proj",frandom(-FireAngle, FireAngle), 1, 6, 4, CMF_AIMDIRECTION, frandom(-FireAngle, FireAngle));
			}
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (9, 1.8, -1.0));
			Actor Case_ = Casings(B_SpawnCasing("RifleCase", (7, -6.5, -3.0)));
			
			if (Case_)
			{
				Case_.A_SetRenderStyle(1.0, STYLE_Shaded);
				Case_.SetShade("b2b2b2");			
			}
			
			
			
		}
		else
		{
			for (int i = 0; i < 5; i++)
			{
				A_FireProjectile("RMD_MolotVPO208Proj",frandom(-IronFireAngle, IronFireAngle), 1, 0, 4, CMF_AIMDIRECTION, frandom(-IronFireAngle, IronFireAngle));
			}
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (7, 0, -0.5));
			Actor Case_ = Casings(B_SpawnCasing("RifleCase", (2, 0, -1.15)));
			
			if (Case_)
			{
				Case_.A_SetRenderStyle(1.0, STYLE_Shaded);
				Case_.SetShade("b2b2b2");			
			}
		}	


		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("MolotVPO208/Fire", CHAN_WEAPON);
		A_StartSound("MolotVPO208/HandlePull", CHAN_WEAPON +1);
		A_AlertMonsters();
	}
	
		
	
	States
	{

	Ready:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;

	Deselect:
		SEL1 FEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 1 A_Lower(int.max);
		Wait;
	
	Select:
		TNT1 A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOSHEL");
				RMD_BARINFO.SetAmmo1("RMD_Shells");				
		}
		TNT1 A 0 A_StartSound("Shotgun/Select", CHAN_WEAPON +4);
		SEL1 ABCDEF 1;
		SEL1 F 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;

	//IronSight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	
	
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.8);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		TNT1 A 0
		{
			Invoker.IronSight = IRON_INACTIVE;
			
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "Reloading");
			}

			Return A_Jump(256, "Ready");			
		}
		Goto Ready;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto Ready;
		
	Fire:
		TNT1 A 0
		{
			if (Invoker.MagLeft(Invoker.Magazine))
			{
				if (Invoker.IronSight == IRON_ACTIVE)
				{
					Return A_Jump(256, "IronFireNow");
				}
				else
				{	
					Return A_Jump(256, "FireNow");				
				}
			}
		
		
			//If it has no ammo left
			if (Invoker.IronSight == IRON_ACTIVE)
			{
				Return A_Jump(256, "IronReady");
			}
			
			else
			{
				Return A_Jump(256, "ReadyReal");				
			}
		}
		Goto Ready;
	
	FireNow:
		FIR1 A 1 FireBullets();
		FIR1 BCDEFGH 1;
		FIR1 H 10;
		FIR1 H 0 A_ReFire("FireNow");
		Goto Ready;
		
	//Ironsight Fire
	IronFireNow:
		IFI1 A 1 FireBullets();
		IFI1 BCDEFGH 1;
		IFI1 H 8;
		IFI1 H 0 A_ReFire("IronFireNow");
		Goto IronReady;
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto Ready;

	ReloadNow:
		//Handle Pull
		REL1 ABCDEFGHIJKLMNO 2;
		TNT1 A 0 
		{
			A_StartSound("MolotVPO208/HandlePull");
		}
		//Clip Insert
		REL1 PQRSTUVWXYZ 2;
		REL2 ABCDEFGHIJKL 2;
		TNT1 A 0
		{
			A_StartSound("MolotVPO208/ClipInsert");
			ReloadMagazine(PRIMARY, Invoker.MagazineMax, Invoker.MagazineMax);	
			RMD_BARINFO.Set_Mag(invoker.Magazine);
		}
		//Handle Push
		REL2 MNOPQRST 2;
		TNT1 A 0 
		{
			A_StartSound("MolotVPO208/HandlePush")	;	
		}
		REL2 UVWXYZ 2;
		REL3 ABCDEF 2;
	Goto ReadyReal;

		
	}
}


Class RMD_MolotVPO208Proj : RMD_BaseBullet
{
	Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 10; 
		RMD_BaseBullet.DamageRand 3.65;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 1000;
		RMD_BaseBullet.ExtremeDeath True;
		
		Scale 1.0;
		Decal "RMD_Buckshot_BulletHoles";
		-NOEXTREMEDEATH;
	}
}