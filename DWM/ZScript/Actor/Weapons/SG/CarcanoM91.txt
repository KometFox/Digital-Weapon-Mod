class RMD_CarcanoM91 : BaseWeapon
{
	//Firing Accuracy
	Const FireAngle = 0.1;
	Const IronFireAngle = 0.035;

	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.SlotNumber 4;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Shells";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "Carcano M91";
		Inventory.AltHudIcon "PICARM91";
		BaseWeapon.IronSight IRON_INACTIVE;

		BaseWeapon.MagazineMax 10;
		BaseWeapon.Magazine 10;
		
	}
	
	Action void FireBullets()
	{	
		//Check for Ammo
		if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_ACTIVE)
		{
			SetWeaponState("IronReady");
			Return;
		}
		else if (Invoker.Magazine <= 0 && Invoker.IronSight == IRON_INACTIVE)
		{
			SetWeaponState("ReadyReal");
			Return;
		}				
			
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			A_FireProjectile("RMD_CarcanoM91Proj",frandom(-FireAngle, FireAngle), 1, 6, 4, CMF_AIMDIRECTION, frandom(-FireAngle, FireAngle));
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (9, 1.8, -1.0));
			Actor Case_ = Casings(B_SpawnCasing("RifleCase", (7, -6.5, 3.0)));
		}
		else
		{
			A_FireProjectile("RMD_CarcanoM91Proj",frandom(-IronFireAngle, IronFireAngle), 1, 0, 4, CMF_AIMDIRECTION, frandom(-IronFireAngle, IronFireAngle));
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (7, 0, -0.5));
			Actor Case_ = Casings(B_SpawnCasing("RifleCase", (2, 0, 1.15)));
		}	


		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("CarcanoM91/Fire", CHAN_WEAPON);
		A_AlertMonsters();
	}
	
		
	
	States
	{

	Ready:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;

	Deselect:
		SEL1 FEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 1 A_Lower(int.max);
		Wait;
	
	Select:
		TNT1 A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOSHEL");
				RMD_BARINFO.SetAmmo1("RMD_Shells");				
		}
		TNT1 A 0 A_StartSound("Shotgun/Select", CHAN_WEAPON +4);
		SEL1 ABCDEF 1;
		SEL1 F 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;

	//IronSight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	
	
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.8);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		TNT1 A 0
		{
			Invoker.IronSight = IRON_INACTIVE;
			
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "Reloading");
			}

			Return A_Jump(256, "Ready");			
		}
		Goto Ready;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto Ready;
		
	Fire:
		TNT1 A 0
		{
			if (Invoker.MagLeft(Invoker.Magazine))
			{
				if (Invoker.IronSight == IRON_ACTIVE)
				{
					Return A_Jump(256, "IronFireNow");
				}
				else
				{	
					Return A_Jump(256, "FireNow");				
				}
			}
		
		
			//If it has no ammo left
			if (Invoker.IronSight == IRON_ACTIVE)
			{
				Return A_Jump(256, "IronReady");
			}
			
			else
			{
				Return A_Jump(256, "ReadyReal");				
			}
		}
		Goto Ready;
	
	FireNow:
		FIR1 A 1 FireBullets();
		FIR1 ABCDEFGHIJLMNOPQRSTU 1;
		FIR1 V 1 A_StartSound("CarcanoM91/Open");
		FIR1 WXYZ 1;
		FIR2 A 1 A_StartSound("CarcanoM91/Close");
		FIR2 BCDEFGHIJKL 1;
		FIR1 H 0 A_ReFire("FireNow");
		Goto Ready;
		
	//Ironsight Fire
	IronFireNow:
		IFI1 A 1 FireBullets();
		IFI1 ABCDEFGHIJK 1;
		IFI1 L 1 A_StartSound("CarcanoM91/Open");
		IFI1 MNOP 1;
		IFI1 Q 1 A_StartSound("CarcanoM91/Close");
		IFI1 RSTUVWXYZ 1;
		IFI2 ABCDEF 1;
		IFI2 F 0 A_ReFire("IronFireNow");
		Goto IronReady;
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto Ready;

	ReloadNow:
		REL1 ABCEDFGHIJKL 2;
		REL1 M 2 A_StartSound("CarcanoM91/Open");
		REL1 NOPQRSTUVWXYZ 2;
		REL2 ABC 2;
		REL2 D 2;
		TNT1 A 0
		{
			A_StartSound("MolotVPO208/ClipInsert");
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
			RMD_BARINFO.Set_Mag(invoker.Magazine);
		}
		REL2 EFGH 2;
		REL2 I 1 A_StartSound("CarcanoM91/Close"); 
		REL2 KLMNOPQ 2;
		
	Goto ReadyReal;

		
	}
}


Class RMD_CarcanoM91Proj : RMD_BaseBullet
{
	Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 120; 
		RMD_BaseBullet.DamageRand 1.6;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 1000;
		RMD_BaseBullet.ExtremeDeath False;
		
		Scale 1.0;
		Decal "RMD_Buckshot_BulletHoles";
	}
}