class SMRL : BaseWeapon
{
	double ScopeZoom;

	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 100;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Rockets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIBRL1";
		Obituary "$OB_MPCHAINGUN";
		Tag "SMRL-4";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 1;
		BaseWeapon.Magazine 1;
	}
	
	Action void FireRocket()
	{
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			A_FireProjectile("SMRL_Rocket", 0, 1, 6, 4, CMF_AIMDIRECTION, 0);
			
			B_MuzzleFlash("RMD_RocketMuzzleFlash", (25, 5.0, 0.0));	
		}
		else
		{
			A_FireProjectile("SMRL_Rocket", 0, 1, 0, 4, CMF_AIMDIRECTION, 0);
			
			B_MuzzleFlash("RMD_RocketMuzzleFlash", (25, 3.0, 0.0));
		}
			
		B_SpawnCasing("RocketCase", (-35, 0.0, 0.0));			
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("SMRL/Fire", CHAN_WEAPON);
		A_AlertMonsters();	
	}


	States
	{

	Ready:
		CHGG A 0;
		TNT1 A 0 
		{
			A_Jump(256, "Init");
		
		}
	
	Init:
		TNT1 A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOROCK");
				RMD_BARINFO.SetAmmo1("RMD_Rockets");
				A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
		}
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
		
	Deselect:
		SELC DCBA 1;
		SELC A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Loop;
		
	Select:
		TNT1 A 0 
		{
				TakeCounterItem();
				A_StartSound("RocketLauncher/Select", CHAN_WEAPON +5);
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMOROCK");
				RMD_BARINFO.SetAmmo1("RMD_Rockets");				
		}
		SELC ABCD 1;
		SELC D 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
	
	Fire:
		TNT1 A 0
		{
			if (Invoker.MagLeft(Invoker.Magazine))
			{
				if (Invoker.IronSight == IRON_ACTIVE)
				{
					Return A_Jump(256, "IronFireNow");
				}
				else
				{	
					Return A_Jump(256, "FireNow");				
				}
			}
		
		
			//If it has no ammo left
			if (Invoker.IronSight == IRON_ACTIVE)
			{
				Return A_Jump(256, "IronReady");
			}
			
			else
			{
				Return A_Jump(256, "ReadyReal");				
			}		
			
		}
		Goto Ready;
		
	IronReady:
		SCOA C 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	IronSightDeselect:
		IDLE A 0 
		{
			A_SetCrosshair(0);
			A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
		}
		IDLE A 7;
		IDLE A 0
		{
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "ReloadNow");
			}

			Return A_Jump(256, "ReadyReal");			
		}
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto Ready;
		
	AltHold:
		SCOA C 1
		{
			if ( Invoker.ScopeZoom <= 8.0 )
			{
				A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
				Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
			}
		}
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;		
		
	FireNow:
		FIRE A 2;
		TNT1 A 0 FireRocket();
		FIRE BCDEF 2;
		IDLE A 0 A_Refire;
		Goto Ready;
		
	IronFireNow:
		SCOA C 2;
		TNT1 C 0 FireRocket();
		SCOA CCCCC 2;
		SCOA C 0 A_Refire;
		Goto IronReady;		
	
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto Ready;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload");
		RELO AB 2;
		TNT1 A 0 A_StartSound("SMRL/PullOut", CHAN_WEAPON +1);
		RELO CD 2; //Launcher Down
		TNT1 A 0 
				{
					A_FireProjectile("RocketCaseSpawn", 180); 
				
				}
		RELO EFGHIJ 2;
		RELO K 35; //Delay
		TNT1 A 0;
		TNT1 A 0 A_StartSound("SMRL/PullIn", CHAN_WEAPON +2);
		RELO LMN 2; //Rocket Insert
		TNT1 A 0
				{
					A_StartSound("SMRL/Reload", CHAN_WEAPON);
				
					ReloadSingle(PRIMARY, 1, 1);				
					RMD_BARINFO.Set_Mag(invoker.Magazine);
				}
		RELO OPQRSTUVWXY 2;
		Goto Ready;

	}
}

Class SMRL_Rocket : HeavyExplosivesFast
{
	const FLARE = SXF_ABSOLUTEVELOCITY + SXF_ABSOLUTEANGLE + SXF_TRANSFERPITCH;

	Default
	{
		Speed 90;
		Decal "RMD_LargeExplosion";
		MissileType "RMD_SMRL_RocketTrail";
		
		//Impact 
		BaseProjectileFast.ImpactDamage 0;
		//Splash 
		BaseProjectileFast.SplashDamage 700;
		BaseProjectileFast.FullSplashDamage 75;
		BaseProjectileFast.SplashRadius 180;
		//Randomizer
		BaseProjectileFast.NerfDamageRandom 1.0;
		BaseProjectileFast.NormalDamageRandom 1.0;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				Actor Flare;
				
				Flare = Spawn("RMD_SMRL_Flare", Pos, ALLOW_REPLACE);
						
				If (Flare)
				{
					Flare.Target = Self;
					Flare.Scale = (0.45, 0.45);
					Flare.SetShade("Steel Blue");	
				}			
			}
			Goto SpawnLoop;
		SpawnLoop:	
			IDLE A 1;
			Loop;
		Explode:
			TNT1 A 0 
			{
				A_StartSound("Explosion/BigRocket", 90, 0, 1.75);
				A_SpawnItemEx("RMD_LargeExplosionSpawner", 0, 0, 0);
			}

			Stop;	
	}
}
