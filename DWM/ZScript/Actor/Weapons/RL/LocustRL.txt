class RMD_LocustRL : BaseWeapon
{
	double ScopeZoom;

	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Grenades";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PILORL";
		Obituary "$OB_MPCHAINGUN";
		Tag "Locust RL";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 6;
		BaseWeapon.Magazine 6;
		
	}

	Action void FireRocket()
	{		
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			A_FireProjectile("RMD_LocustRL_Rocket", 0, 1, 6, 4, CMF_AIMDIRECTION,0);
			B_MuzzleFlash("RMD_GrenadeMuzzleFlash", (10, 3.3, -1.95));	
		}
		else
		{
			A_FireProjectile("RMD_LocustRL_Rocket", 0, 1, 0, 4, CMF_AIMDIRECTION,0);
			B_MuzzleFlash("RMD_GrenadeMuzzleFlash", (7, 0.0, -1.0));	
		}

		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("LocustRL/Fire", CHAN_WEAPON);
		A_AlertMonsters();	
	}


	States
	{

	Ready:
		CHGG A 0;
		TNT1 A 0 
		{
			A_Jump(256, "Init");
		
		}
		Goto Init;

	Init:
		TNT1 A 0 
		{
				TakeCounterItem();
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMONADE");
				RMD_BARINFO.SetAmmo1("RMD_Grenades");
				A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
		}
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	

	Deselect:
		SEL1 HGFEDCBA 1 A_Lower;
		SEL1 A 1 A_Lower(int.max);
		Wait;
		
	Select:
		TNT1 A 0 
		{
				A_StartSound("RocketLauncher/Select", CHAN_WEAPON +5);
				A_TakeInventory("Reloading", 99);
				RMD_BARINFO.Set_Mag(invoker.Magazine);
				RMD_BARINFO.SetAmmoIcon("AMMONADE");
				RMD_BARINFO.SetAmmo1("RMD_Grenades");
		}
		SEL1 ABCDEFGH 1 A_Raise();
		SEL1 H 1 A_Raise(int.max);
        Wait;
			
	//Ironsight Mode
	IronReady:
		SCOA C 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	IronSightDeselect:
		IDLE A 0 
		{
			A_SetCrosshair(0);
			A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
		}
		IDLE A 7;
		IDLE A 0
		{
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "ReloadNow");
			}

			Return A_Jump(256, "ReadyReal");			
		}
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto Ready;
		
	AltHold:
		SCOA C 1
		{
			if ( Invoker.ScopeZoom <= 8.0 )
			{
				A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
				Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
			}
		}
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;
		
	Fire:
		TNT1 A 0
		{
		
			if (Invoker.MagLeft(Invoker.Magazine))
			{
				if (Invoker.IronSight == IRON_ACTIVE)
				{
					Return A_Jump(256, "IronFireNow");
				}
				else
				{	
					Return A_Jump(256, "FireNow");				
				}
			}
	
			//If it has no ammo left
			if (Invoker.IronSight == IRON_ACTIVE)
			{
				Return A_Jump(256, "IronReady");
			}
			
			else
			{
				Return A_Jump(256, "ReadyReal");				
			}		
			
		}
		Goto ReadyReal;
		
	FireNow:
		FIR1 A 1 FireRocket();
		FIR1 BCDEFGHIJKLMNOPQRUVWX 1;
		FIR1 R 0 A_ReFire;
		Goto ReadyReal;
		
	IronFireNow:
		SCOA C 1 FireRocket();
		SCOA CCCCCCCCCCCCCCCCCCCCC 1;
		SCOA C 0 A_ReFire;
		Goto IronReady;
	
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
		
	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		REL1 ABCDEFGHIJK 2;
		TNT1 A 0
		{
			A_StartSound("LocustRL/MagOut", CHAN_WEAPON);
		}
		REL1 LMNOPQRSTUVWXYZ 2;
		REL2 ABCDEFGHI 2;
		TNT1 A 0
		{
			A_StartSound("LocustRL/MagIn", CHAN_WEAPON +1);
			
			ReloadMagazine(PRIMARY, Invoker.MagazineMax, Invoker.MagazineMax);			
			Invoker.Magazine = Invoker.MagazineMax;
			RMD_BARINFO.Set_Mag(invoker.Magazine);
		}
		REL2 JKLMN 2;
		Goto ReadyReal;
		
	}
}

Class RMD_LocustRL_Rocket : HeavyExplosivesFast
{
	Default
	{
		Speed 120;
		Decal "RMD_SmallExplosion";
		MissileType "RMD_LocustRocketTrail"; 
		
		//Impact 
		BaseProjectileFast.ImpactDamage 0;
		//Splash 
		BaseProjectileFast.SplashDamage 230;
		BaseProjectileFast.FullSplashDamage 70;
		BaseProjectileFast.SplashRadius 130;
		
		BaseProjectileFast.NormalDamageRandom 1.0;
		BaseProjectileFast.NerfDamageRandom 1.0;
		
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				Actor Flare;
						
				Flare = Spawn("RMD_SMRL_Flare", Pos, ALLOW_REPLACE);
						
				If (Flare)
				{
					Flare.Target = Self;
					Flare.Scale = (0.23, 0.23);
					Flare.SetShade("Lavender");	
				}			
			}
			Goto SpawnLoop;
		SpawnLoop:	
			IDLE A 1;
			Loop;
		Explode:
			TNT1 A 0 
			{
				A_StartSound("Explosion/Rocket", 90, 0, 1.5);
				A_SpawnItemEx("RMD_40mmExplosionSpawner", 0, 0, 0);
			}
			Stop;	
	}
}


