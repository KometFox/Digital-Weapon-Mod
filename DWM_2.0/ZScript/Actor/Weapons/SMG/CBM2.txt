class CBM3 : BaseWeapon
{
	Default
	{
		Weapon.SelectionOrder 2;
		Weapon.SlotNumber 3;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_PistolBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PICBM3";
		Obituary "$OB_MPCHAINGUN";
		Tag "CB-M2";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 44;
		BaseWeapon.Magazine 44;
		
	}

	Action void FireBullets()
	{
		//CB-M2 uses a caseless 9mm ammo 
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			//A_FireProjectile("RMD_CBM3_Projectile",frandom(-0.7, 0.7), 1, 6, 4, CMF_AIMDIRECTION,frandom(-0.5, 0.5));
			A_FireBullets(0.7, 0.7, -1, 14, "RMD_BulletImpact", FBF_NoRandom); 
		
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (13, 2.7, -2.0));	
			//B_SpawnCasing("PistolCase", (12, -8.5, -2.0));
		}
		else
		{
			//A_FireProjectile("RMD_CBM3_Projectile",frandom(-0.4, 0.4), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.4, 0.4));
			A_FireBullets(0.35, 0.35, -1, 14, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (13, 0.0, -1.5));	
			//B_SpawnCasing("PistolCase", (12, -8.5, -2.0));
		}

		
		A_StartSound("CBM2/Fire", CHAN_WEAPON);
		A_AlertMonsters();
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
	}

	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOPIST";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}

	States
	{

	Ready:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;

	Deselect:
		SEL1 FEDCBA 1 A_Lower;
		SEL1 A 1 A_Lower;
		TNT1 A 0 A_Lower(100);
		Loop;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SEL1 ABCDEF 1 A_Raise;
		IDLE A 1 A_Raise(100);
        Goto Ready;
			
	//Ironsight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
	
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.5);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		Goto Ready;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto Ready;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto Ready;
	
	FireNow:	
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 1 FireBullets();
		FIR1 BC 1;
		FIR1 D 0 A_ReFire;
		FIR1 DEF 1; 
		Goto Ready;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 1 FireBullets();
		IFI1 BC 1;
		IFI1 D 0 A_ReFire;
		IFI1 DEF 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto Ready;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		REL1 ABCDEFGH 2;
		TNT1 A 0;
		TNT1 A 0
		{
			A_StartSound("MP5/MagOut", CHAN_WEAPON);
			A_FireProjectile("SMGMagSpawn",-5,0,8,-4);
		}
		REL1 IJKLMNOP 2;
		TNT1 A 0
		{
			A_StartSound("MP5/MagIn", CHAN_WEAPON +1);	
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
		}
		REL1 QRSTUV 2;
		Goto Ready;
		
	}
}


Class RMD_CBM3_Projectile : RMD_BaseBullet
{
	Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 6; 
		RMD_BaseBullet.DamageRand 1.4;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 220;				
		
		Scale 0.75;
		Decal "RMD_SMG_BulletHoles";
	}
	
 
}