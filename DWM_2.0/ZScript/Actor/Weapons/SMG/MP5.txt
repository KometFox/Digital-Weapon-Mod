class MP5 : BaseWeapon
{
	const Primary_AmmoU = 2;
	const STAB = 50; //Stability

	Default
	{
		Weapon.SelectionOrder 1;
		Weapon.SlotNumber 3;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_PistolBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIMP5";
		Obituary "$OB_MPCHAINGUN";
		Tag "MP5";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 60;
		BaseWeapon.Magazine 60;
		
	}

	Action void FireBullets()
	{ 
		if (invoker.IronSight == IRON_INACTIVE)
		{

			//A_FireProjectile("RMD_MP5_Projectile",frandom(-0.5, 0.5), 1, 6, 4, CMF_AIMDIRECTION, frandom(-0.5, 0.5));
			A_FireBullets(0.5, 0.5, -1, 8, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (13, 2.7, -2.0));	
			B_SpawnCasing("PistolCase", (12, -15.0, 3.0));
		}
			
		else
		{
			//A_FireProjectile("RMD_MP5_Projectile",frandom(-0.25, 0.25), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.25, 0.25));
			A_FireBullets(0.25, 0.25, -1, 8, "RMD_BulletImpact", FBF_NoRandom);
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (13, 0.0, -1.2));	
			B_SpawnCasing("PistolCase", (12, 0.0, -2.0));
		}

		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
				
		A_StartSound("MP5/Fire", CHAN_WEAPON);
		A_AlertMonsters();
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOPIST";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}

	States
	{
		
	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		IDLE A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;

	Deselect:
		SEL1 GFEDCBA 1 A_Lower;
		SEL1 A 1 A_Lower;
		TNT1 A 0 A_Lower(100);
		Loop;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SEL1 ABCDEFG 1 A_Raise;
		IDLE A 1 A_Raise(100);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_NOBOB);
		Loop;
		
	IronSightAnim:
		IRO1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.5);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto ReadyReal;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 1 FireBullets();
		FIR1 BC 1;
		IDLE A 0 A_ReFire;
		FIR1 DEFGH 1;
		Goto ReadyReal;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 1 FireBullets();
		IFI1 BC 1;
		IFI1 A 0 A_ReFire;
		IFI1 DEFGH 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		REL1 ABCDEFGHIJKLMNO 2;
		TNT1 A 0
		{
			A_StartSound("MP5/MagOut", CHAN_WEAPON);
			A_FireProjectile("SMGMagSpawn",-5,0,8,-4);
		}
		REL1 PQRSTUVWXYZ 2;
		REL2 AB 2;
		TNT1 A 0
		{
			A_StartSound("MP5/MagIn", CHAN_WEAPON +1);		
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
		}
		REL2 CDEFGHI 2;		
		TNT1 A 0 A_StartSound("MP5/Cocking", CHAN_WEAPON);
		REL2 JKLMNOPQRSTUVWXYZ 2;
		Goto ReadyReal;

	}
}


Class RMD_MP5_Projectile : RMD_BaseBullet
{

Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 7;
		RMD_BaseBullet.DamageRand 1.6;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 275;		
		
		Scale 0.75;
		Decal "RMD_SMG_BulletHoles";
	}
}