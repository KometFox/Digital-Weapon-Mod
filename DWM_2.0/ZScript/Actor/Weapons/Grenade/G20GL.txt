class RMD_KestrelGL : BaseWeapon
{

	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Explosives";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIG20";
		Obituary "$OB_MPCHAINGUN";
		Tag "Kestrel GL";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 10;
		BaseWeapon.Magazine 10;
		
	}

	Action void FireGrenade()
	{
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			A_FireProjectile("RMD_G20GL_Grenade", 0, 1, 0, 4, CMF_AIMDIRECTION,0);
			B_MuzzleFlash("RMD_GrenadeMuzzleFlash", (15, 3.3, -1.6));	
		}
		else
		{
			A_FireProjectile("RMD_G20GL_Grenade", 0, 1, 0, 4, CMF_AIMDIRECTION,0);
			B_MuzzleFlash("RMD_GrenadeMuzzleFlash", (15, 0.0, -1.0));	
		}

		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("Kestrel/Fire", CHAN_WEAPON);
		A_AlertMonsters();	
	}

	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMONADE";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;

		InitWeapon(CIWpn);	
	}
	
	States
	{

	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	

	Deselect:
		SELC GFEDCBA 1;
		SELC A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Loop;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SELC ABCDEFG 1;
		SELC G 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronReady:
		IRON D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
		
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRON ABCD 2 A_ZoomFactor(1.5);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRON DCBA 2 A_ZoomFactor(1.0);
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto ReadyReal;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIRE A 1 FireGrenade();
		FIRE BC 1;
		FIRE DEF 3;
		FIRE F 8;
		FIRE A 0 A_ReFire;
		Goto ReadyReal;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFIR A 1 FireGrenade();
		IFIR BC 1;
		IFIR DEF 3;
		IFIR F 8;
		IFIR A 0 A_ReFire;
		Goto IronReady;	
	
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
		
	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		RELO ABCDEF 2;
		TNT1 A 0
		{
			A_StartSound("Kestrel/MagOut", CHAN_WEAPON);
		}
		RELO GHI 2;
		RELO J 15;
		TNT1 A 0;
		RELO K 2;
		TNT1 A 0
		{
			A_StartSound("Kestrel/MagIn", CHAN_WEAPON +1);
			
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
		}
		RELO MNOPQRS 2;		
		Goto ReadyReal;
		

	}
}


Class RMD_G20GL_Grenade : LightExplosives
{
	Default
	{
		Speed 50;
		Decal "RMD_SmallExplosion";
		Gravity 0.5;
		//Impact 
		BaseProjectile.ImpactDamage 0;
		//Splash 
		BaseProjectile.SplashDamage 275;
		BaseProjectile.FullSplashDamage 95;
		BaseProjectile.SplashRadius 200;
		
		BaseProjectile.NormalDamageRandom 1.0;
		BaseProjectile.NerfDamageRandom 1.0;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				Actor Flare;
						
				Flare = Spawn("RMD_SMRL_Flare", Pos, ALLOW_REPLACE);
						
				If (Flare)
				{
					Flare.Target = Self;
					Flare.Scale = (0.23, 0.23);
					Flare.SetShade("Lavender");	
				}			
			}
			Goto SpawnLoop;
		SpawnLoop:	
			IDLE A 1 A_SpawnItemEx("RMD_GrenadeTrail", 0, 0, 0);
			Loop;
		Explode:
			TNT1 A 0 
			{
				A_StartSound("Explosion/Small");
				A_SpawnItemEx("RMD_40mmExplosionSpawner", 0, 0, 0);
			}
			Stop;	
	}
}

