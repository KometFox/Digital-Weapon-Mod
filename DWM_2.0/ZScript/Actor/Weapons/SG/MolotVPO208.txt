class RMD_MolotVPO208 : BaseWeapon
{
	//Firing Accuracy
	Const FireAngle = 0.08;
	Const IronFireAngle = 0.04;
	Const AmmoConsum = 3;

	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.SlotNumber 4;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_MediumBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "Molot VPO-208";
		Inventory.AltHudIcon "PIMOL208";
		BaseWeapon.IronSight IRON_INACTIVE;

		BaseWeapon.MagazineMax 16;
		BaseWeapon.Magazine 16;
		
	}
	
	Action void FireBullets()
	{	
		if (Invoker.IronSight == IRON_INACTIVE)
		{

			//A_FireProjectile("RMD_MolotVPO208Proj",frandom(-FireAngle, FireAngle), 1, 6, 4, CMF_AIMDIRECTION, frandom(-FireAngle, FireAngle));
			A_FireBullets(0.08, 0.08, -1, 185, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (9, 1.8, -1.0));
			Actor Case_ = Casings(B_SpawnCasing("RifleCase", (7, -6.5, -3.0)));
			
			if (Case_)
			{
				Case_.A_SetRenderStyle(1.0, STYLE_Shaded);
				Case_.SetShade("b2b2b2");			
			}			
			
		}
		else
		{
			//A_FireProjectile("RMD_MolotVPO208Proj",frandom(-IronFireAngle, IronFireAngle), 1, 0, 4, CMF_AIMDIRECTION, frandom(-IronFireAngle, IronFireAngle));
			A_FireBullets(0.04, 0.04, -1, 140, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (7, 0, -0.5));
			Actor Case_ = Casings(B_SpawnCasing("RifleCase", (2, 0, -1.15)));
			
			if (Case_)
			{
				Case_.A_SetRenderStyle(1.0, STYLE_Shaded);
				Case_.SetShade("b2b2b2");			
			}
		}	


		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("MolotVPO208/Fire", CHAN_WEAPON);
		A_StartSound("MolotVPO208/HandlePull", CHAN_WEAPON +1);
		A_AlertMonsters();
		B_WpnRecoil(0.95);
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOAR";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}
		
	
	States
	{

	Ready:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;

	Deselect:
		SEL1 FEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 1 A_Lower(int.max);
		Wait;
	
	Select:
		TNT1 A 0 B_InitWeapon();
		TNT1 A 0 A_StartSound("Shotgun/Select", CHAN_WEAPON +4);
		SEL1 ABCDEF 1;
		SEL1 F 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;

	//IronSight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	
	
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.8);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		Goto Ready;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto Ready;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto Ready;
	
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 2 FireBullets();
		FIR1 BCDEFGH 2;
		FIR1 H 5;
		FIR1 H 0 A_ReFire("FireNow");
		Goto Ready;
		
	//Ironsight Fire
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 2 FireBullets();
		IFI1 BCDEFGH 2;
		IFI1 H 5;
		IFI1 H 0 A_ReFire("IronFireNow");
		Goto IronReady;
		
	Reload:
		TNT1 A 0 ReloadCheck(3);
		Goto Ready;

	ReloadNow:
		//Handle Pull
		REL1 ABCDEF 2;
		TNT1 A 0 
		{
			A_StartSound("MolotVPO208/HandlePull");
		}
		//Clip Insert
		REL1 GHIJK 2;
		TNT1 A 0
		{
			A_StartSound("MolotVPO208/ClipInsert");
			ReloadGun(Invoker.AmmoType1, 0, 1, AmmoConsum);
		}
		//Handle Push
		REL1 LMNO 2;
		TNT1 A 0 
		{
			A_StartSound("MolotVPO208/HandlePush")	;	
		}
		REL1 PQRSTUVWXYZ 2;
	Goto ReadyReal;

		
	}
}


Class RMD_MolotVPO208Proj : RMD_BaseBullet
{
	Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 140; 
		RMD_BaseBullet.DamageRand 1.4;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 1000;
		
		Scale 1.0;
		Decal "RMD_Buckshot_BulletHoles";
	}
}