class RMD_CarcanoM91 : BaseWeapon
{
	//Firing Accuracy
	Const FireAngle = 0.1;
	Const IronFireAngle = 0.035;

	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.SlotNumber 4;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_MediumBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "Carcano M91";
		Inventory.AltHudIcon "PICARM91";
		BaseWeapon.IronSight IRON_INACTIVE;

		BaseWeapon.MagazineMax 10;
		BaseWeapon.Magazine 10;
		
	}
	
	Action void FireBullets()
	{	
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			//A_FireProjectile("RMD_CarcanoM91Proj",frandom(-FireAngle, FireAngle), 1, 6, 4, CMF_AIMDIRECTION, frandom(-FireAngle, FireAngle));
			A_FireBullets(0.07, 0.07, -1, 340, "RMD_BulletImpact", FBF_NoRandom);
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (9, 1.8, -1.0));
		}
		else
		{
			//A_FireProjectile("RMD_CarcanoM91Proj",frandom(-IronFireAngle, IronFireAngle), 1, 0, 4, CMF_AIMDIRECTION, frandom(-IronFireAngle, IronFireAngle));
			A_FireBullets(0.05, 0.05, -1, 340, "RMD_BulletImpact", FBF_NoRandom);
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (7, 0, -0.5));
		}	


		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("CarcanoM91/Fire", CHAN_WEAPON);
		A_AlertMonsters();
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOAR";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}
	
	States
	{

	Ready:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;

	Deselect:
		SEL1 FEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 1 A_Lower(int.max);
		Wait;
	
	Select:
		TNT1 A 0 B_InitWeapon();
		TNT1 A 0 A_StartSound("Shotgun/Select", CHAN_WEAPON +4);
		SEL1 ABCDEF 1;
		SEL1 F 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;

	//IronSight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	
	
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.8);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		Goto Ready;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto Ready;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto Ready;
	
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 2 FireBullets();
		FIR1 BCDEFGHIJL 2;
		FIR1 L 0 A_StartSound("CarcanoM91/Open");
		FIR1 L 0 B_SpawnCasing("RifleCase", (2, -6, 1.15));
		FIR1 MN 2;
		FIR1 N 0 A_StartSound("CarcanoM91/Close");
		FIR1 OPQRST 2;
		FIR1 T 0 A_ReFire("FireNow");
		Goto Ready;
		
	//Ironsight Fire
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 2 FireBullets();
		IFI1 BCDEFGHIJL 2;
		IFI1 L 0 A_StartSound("CarcanoM91/Open");
		IFI1 L 0 B_SpawnCasing("RifleCase", (2, 0, 1.15));
		IFI1 MN 2;
		IFI1 N 0 A_StartSound("CarcanoM91/Close");
		IFI1 OPQRST 2;
		IFI1 F 0 A_ReFire("IronFireNow");
		Goto IronReady;
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto Ready;

	ReloadNow:
		REL1 ABCEDFGHI 2;
		REL1 I 0 A_StartSound("CarcanoM91/Open");
		REL1 JKLMNOPQRS 2;
		TNT1 A 0
		{
			A_StartSound("MolotVPO208/ClipInsert");
			ReloadGun(Invoker.AmmoType1, 0, 1, 5);
		}
		REL1 TUVWXYZ 2;
		REL1 Z 0 A_StartSound("CarcanoM91/Close"); 
		REL2 ABCDEFGH 2;
		
	Goto ReadyReal;

		
	}
}


Class RMD_CarcanoM91Proj : RMD_BaseBullet
{
	Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 250; 
		RMD_BaseBullet.DamageRand 1.4;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 1000;
		
		Scale 1.0;
		Decal "RMD_Buckshot_BulletHoles";
	}
}