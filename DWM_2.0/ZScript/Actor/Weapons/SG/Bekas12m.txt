class RMD_Bekas12m : BaseWeapon
{
	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.SlotNumber 4;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Shells";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "Bekas 12m";
		Inventory.AltHudIcon "PIBEK12";
		BaseWeapon.IronSight IRON_INACTIVE;

		BaseWeapon.MagazineMax 8;
		BaseWeapon.Magazine 8;
		
	}
	
	Action void FireBullets()
	{	
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			//A_FireProjectile("RMD_Bekas12mProj",frandom(-2.85, 2.85), 1, 6, 4, CMF_AIMDIRECTION, frandom(-2.85, 2.85));
			A_FireBullets(2.85, 2.85, 9, 20, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (9, 1.8, -1.0));	
		}
		else
		{
			//A_FireProjectile("RMD_Bekas12mProj",frandom(-1.0, 1.0), 1, 0, 4, CMF_AIMDIRECTION, frandom(-1.0, 1.0));
			A_FireBullets(1.0, 1.0, 9, 20, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BuckshotMuzzleFlash", (7, 0, -0.5));	
		}	


		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("Bekas12m/Fire", CHAN_WEAPON);
		A_AlertMonsters();
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOSHEL";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}
	
	States
	{

	Ready:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;

	Deselect:
		SEL1 HGFEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 1 A_Lower(int.max);
		Wait;
	
	Select:
		TNT1 A 0 B_InitWeapon();
		TNT1 A 0 A_StartSound("Shotgun/Select", CHAN_WEAPON +4);
		SEL1 ABCDEFGH 1;
		SEL1 H 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;

	//IronSight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	
	
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(1.8);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		Goto Ready;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto Ready;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto Ready;
	
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 2 FireBullets();
		FIR1 BCDEF 2;
		FIR1 G 0 A_StartSound("Bekas12m/CockStart", CHAN_WEAPON +1);
		FIR1 G 0 B_SpawnCasing("ShellCase", (4, -6.5, -3.0));		
		FIR1 GH 2;
		FIR1 H 0 A_StartSound("Bekas12m/CockEnd", CHAN_WEAPON +2);
		FIR1 IJKLM 2;
		IDLE A 0 A_Refire("FireNow");
		Goto Ready;
		
	//Ironsight Fire
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 1 FireBullets();
		IFI1 BCDE 2;
		IFI1 E 0 A_StartSound("Bekas12m/CockStart", CHAN_WEAPON +1); 
		IFI1 E 0 B_SpawnCasing("ShellCase", (4, -0.5, 2.7));
		IFI1 GH 2;
		IFI1 H 0 A_StartSound("Bekas12m/CockEnd", CHAN_WEAPON +2); 
		IFI1 IJKLM 2;
		IFI1 O 0 A_ReFire("IronFireNow");
		Goto IronReady;
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto Ready;

	ReloadNow:
		REL1 ABC 2;
		REL1 F 0;
		Goto ReloadLoop; 
	
	ReloadLoop: 
		TNT1 A 0 
		{
			if (Player.mo.CountInv(Invoker.AmmoType1) <= 0)
			{
				Return ResolveState("ReloadEnd");
			}
			
			Return A_Jump(0, "ReloadEnd");
		}
		//Start Loop
		REL1 DEFG 3 A_WeaponReady();
		TNT1 A 0
		{
			A_StartSound("Bekas12m/Insert", CHAN_WEAPON);
			
			ReloadGun(Invoker.AmmoType1, 0, 1, 1, True);
			
			if (invoker.Magazine == invoker.MagazineMax)
			{
				Return ResolveState("ReloadEnd");
			}
			
			Return ResolveState("ReloadLoop");
		}
		REL1 HIJK 3 A_WeaponReady();
		Goto ReloadEnd;	
	

	ReloadEnd:
		REL1 KLM 2;
		REL1 O 0 A_StartSound("Bekas12m/CockEnd", CHAN_WEAPON +1);
		Goto Ready; 	
	}
}


Class RMD_Bekas12mProj : RMD_BaseBullet
{
	Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 23; 
		RMD_BaseBullet.DamageRand 1.6;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 1000;
		
		Scale 1.0;
		Decal "RMD_Buckshot_BulletHoles";
	}
}