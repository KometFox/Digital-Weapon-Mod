class RMD_SMAW : BaseWeapon
{
	double ScopeZoom;
	const AmmoUsage = 5;

	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 100;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Explosives";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PISMAW";
		Obituary "$OB_MPCHAINGUN";
		Tag "Mk-153 SMAW";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 1;
		BaseWeapon.Magazine 1;
	}
	
	Action void FireRocket()
	{
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			A_FireProjectile("SMAW_Rocket", 0, 1, 6, 4, CMF_AIMDIRECTION, 0);
			
			B_MuzzleFlash("RMD_RocketMuzzleFlash", (25, 5.0, 0.0));	
		}
		else
		{
			A_FireProjectile("SMAW_Rocket", 0, 1, 0, 4, CMF_AIMDIRECTION, 0);
			
			B_MuzzleFlash("RMD_RocketMuzzleFlash", (25, 3.0, 0.0));
		}
			
		B_SpawnCasing("RocketCase", (-35, 0.0, 0.0));			
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("SMRL/Fire", CHAN_WEAPON);
		A_AlertMonsters();	
		B_WpnRecoil(1.4);
	}
		
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMONADE";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		Invoker.ScopeZoom = 1.0;
		
		InitWeapon(CIWpn);	
	}
	
	Action void ZoomIn()
	{
		if (Invoker.ScopeZoom <= 8.0)
		{
			A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
			Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
		}
	}
	
	Action void ZoomOut()
	{
		A_SetCrosshair(0);
		A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
	}

	States
	{

	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
		
	Deselect:
		SELC DCBA 1;
		SELC A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Loop;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SELC ABCD 1;
		SELC D 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
	
	Fire:
		TNT1 A 0 FireCheck();
		Goto Ready;
		
	IronReady:
		SCOA C 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	IronSightDeselect:
		IDLE A 0 ZoomOut();
		IDLE A 7;
		IDLE A 0;
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto Ready;
		
	AltHold:
		SCOA C 1 ZoomIn();
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;		
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 2;
		TNT1 A 0 FireRocket();
		FIR1 BCDEFGHIJ 2;
		IDLE A 0 A_Refire;
		Goto Ready;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		SCOA C 2;
		TNT1 C 0 FireRocket();
		SCOA CCCCCCCCC 2;
		SCOA C 0 A_Refire;
		Goto IronReady;		
	
	Reload:
		TNT1 A 0 ReloadCheck(2);
		Goto Ready;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload");
		TNT1 A 0 A_StartSound("SMRL/PullOut", CHAN_WEAPON +1);
		REL1 ABCD 9;
		TNT1 A 0;
		TNT1 A 0 A_StartSound("SMRL/PullIn", CHAN_WEAPON +2);
		REL1 E 3; //Rocket Insert
		TNT1 A 0
		{
			A_StartSound("SMRL/Reload", CHAN_WEAPON);	
			ReloadGun(Invoker.AmmoType1, 0, 1, AmmoUsage);
		}
		TNT1 A 0;
		REL1 FGHIJKLMNOPQRST 3;
		Goto Ready;
	}
}

Class SMAW_Rocket : HeavyExplosivesFast
{
	const FLARE = SXF_ABSOLUTEVELOCITY + SXF_ABSOLUTEANGLE + SXF_TRANSFERPITCH;

	Default
	{
		Speed 75;
		Decal "RMD_LargeExplosion";
		MissileType "RMD_SMRL_RocketTrail";
		
		//Impact 
		BaseProjectileFast.ImpactDamage 0;
		//Splash 
		BaseProjectileFast.SplashDamage 1200;
		BaseProjectileFast.FullSplashDamage 120;
		BaseProjectileFast.SplashRadius 275;
		//Randomizer
		BaseProjectileFast.NerfDamageRandom 1.0;
		BaseProjectileFast.NormalDamageRandom 1.0;
		+ExtremeDeath;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay;
			Goto SpawnLoop;
		SpawnLoop:	
			IDLE A 1;
			Loop;
		Explode:
			TNT1 A 0 
			{
				A_StartSound("Explosion/Large");
				A_SpawnItemEx("RMD_LargeExplosionSpawner", 0, 0, 0);
			}

			Stop;	
	}
}
