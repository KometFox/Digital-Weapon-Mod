class RMD_LocustRL : BaseWeapon
{
	double ScopeZoom;

	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Explosives";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PILORL";
		Obituary "$OB_MPCHAINGUN";
		Tag "Locust RL";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 6;
		BaseWeapon.Magazine 6;
		
	}

	Action void FireRocket()
	{		
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			A_FireProjectile("RMD_LocustRL_Rocket", 0, 1, 6, 4, CMF_AIMDIRECTION,0);
			B_MuzzleFlash("RMD_GrenadeMuzzleFlash", (10, 3.3, -1.95));	
		}
		else
		{
			A_FireProjectile("RMD_LocustRL_Rocket", 0, 1, 0, 4, CMF_AIMDIRECTION,0);
			B_MuzzleFlash("RMD_GrenadeMuzzleFlash", (7, 0.0, -1.0));	
		}

		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		A_StartSound("LocustRL/Fire", CHAN_WEAPON);
		A_AlertMonsters();	
		B_WpnRecoil(0.6);
	}

	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMONADE";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		Invoker.ScopeZoom = 1.0;
		
		InitWeapon(CIWpn);	
	}
	
	Action void ZoomIn()
	{
		if (Invoker.ScopeZoom <= 8.0)
		{
			A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
			Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
		}
	}
	
	Action void ZoomOut()
	{
		A_SetCrosshair(0);
		A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
	}
	
	States
	{

	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	

	Deselect:
		SEL1 HGFEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 1 A_Lower(int.max);
		Wait;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SEL1 ABCDEFGH 1;
		SEL1 H 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronReady:
		SCOA C 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	IronSightDeselect:
		IDLE A 0 ZoomOut();
		IDLE A 7;
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto Ready;
		
	AltHold:
		SCOA C 1 ZoomIn();
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 1 FireRocket();
		FIR1 BCDEFGHIJKLMNOPQ 1;
		FIR1 R 0 A_ReFire;
		Goto ReadyReal;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		SCOA C 1 FireRocket();
		SCOA CCCCCCCCCCCCCCCC 1;
		SCOA C 0 A_ReFire;
		Goto IronReady;
	
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
		
	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		REL1 ABCDEFGHIJK 2;
		TNT1 A 0
		{
			A_StartSound("LocustRL/MagOut", CHAN_WEAPON);
		}
		REL1 LMNOPQRSTUVWXYZ 2;
		REL2 ABCDEFGHI 2;
		TNT1 A 0
		{
			A_StartSound("LocustRL/MagIn", CHAN_WEAPON +1);	
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
		}
		REL2 JKLMN 2;
		Goto ReadyReal;
		
	}
}

Class RMD_LocustRL_Rocket : HeavyExplosives
{
	Default
	{
		Speed 45;
		Decal "RMD_SmallExplosion";

		//Impact 
		BaseProjectile.ImpactDamage 0;
		//Splash 
		BaseProjectile.SplashDamage 330;
		BaseProjectile.FullSplashDamage 100;
		BaseProjectile.SplashRadius 130;
		
		BaseProjectile.NormalDamageRandom 1.0;
		BaseProjectile.NerfDamageRandom 1.0;
		
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				/*
				Actor Flare;
				
				Flare = Spawn("RMD_SMRL_Flare", Pos, ALLOW_REPLACE);
						
				If (Flare)
				{
					Flare.Target = Self;
					Flare.Scale = (0.12, 0.12);
					Flare.SetShade("Lavender");	
				}
				*/
			}
			Goto SpawnLoop;
		SpawnLoop:	
			IDLE A 1 A_SpawnItemEx("RMD_LocustRocketTrail", 0, 0, 0);
			Loop;
		Explode:
			TNT1 A 0 
			{
				A_StartSound("Explosion/Small");
				A_SpawnItemEx("RMD_40mmExplosionSpawner", 0, 0, 0);
			}
			Stop;	
	}
}


