class RMD_AR70 : BaseWeapon
{
	double ScopeZoom;

	Default
	{
		Weapon.SlotNumber 5;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType1 "RMD_MediumBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIAR70";
		Obituary "$OB_MPCHAINGUN";
		Tag "Raptor AR-70";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 100;
		BaseWeapon.Magazine 100;	
	}
	
	Action state ReloadCheck()
	{
		return B_ReloadGoTo(
							"ReadyReal", 
							"IronReady", 
							"IronSightDeselect", 
							"ReloadNow", 
							Invoker.AmmoType1, 
							Invoker.Magazine,
							Invoker.MagazineMax, 
							1);
	}
	
	//Blam blam
	Action Void FireBullets()
	{
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
			

			
		if (Invoker.IronSight == IRON_INACTIVE)
		{	
			//A_FireProjectile("RMD_AR70_Bullet",frandom(-0.75, 0.75), 1, 6, 4, CMF_AIMDIRECTION,frandom(-0.75, 0.75));
			A_FireBullets(0.75, 0.75, -1, 20, "RMD_BulletImpact", FBF_NoRandom); 

			B_MuzzleFlash("RMD_BulletMuzzleFlash", (15, 3.75, -2.0));	
			B_SpawnCasing("RifleCase", (12, -15.0, -1.0));
		}
		else if (Invoker.IronSight == IRON_ACTIVE)
		{
			//A_FireProjectile("RMD_AR70_Bullet",frandom(-0.15, 0.15), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.15, 0.15));
			A_FireBullets(0.15, 0.15, -1, 20, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (10, 0.0, -1.0));
			B_SpawnCasing("RifleCase", (12, 16.0, -0.5));
		}

		A_AlertMonsters();
		A_StartSound("AR70/Fire", CHAN_WEAPON);
		
		B_WpnRecoil(0.22);
	}

	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOPIST";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}


	States
	{

	
	Init:
		IDLE A 0 B_InitWeapon();
		Goto ReadyReal;
	
	ReadyReal:
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	//Ironsight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	Deselect:
		SELC HGFEDCBA 1;
		SELC A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Loop;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SELC ABCDEFGH 1;
		SELC H 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;

	//Ironsight Mode
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(2.25);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto ReadyReal;

	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;

	//Bullets
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 1 FireBullets();
		FIR1 BCD 1;
		FIR1 D 0 A_ReFire("FireNow");
		FIR1 EFG 1;
		Goto ReadyReal;
	
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 1 FireBullets();
		IFI1 ABC 1;
		IFI1 C 0 A_ReFire("IronFireNow");
		IFI1 EFG 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
		
	//The Magazine
	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 99);
		REL1 ABCDEFGHIJ 2;
		TNT1 A 0
		{
			A_StartSound("AR70/MagOut", CHAN_WEAPON +1);
			A_FireProjectile("EmptyMagSpawn",-5,0,8,-4);
		}
		REL1 KLMNOPQRST 2;
		TNT1 A 0		
		{
			A_StartSound("AR70/MagIn", CHAN_WEAPON +2);
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
		}
		REL1 UVWXYZ 2;
		TNT1 A 0 
		{
			A_StartSound("AR70/Cocking", CHAN_WEAPON +2);
		}
		REL2 ABCDEFGHIJKLMNOPQ 2;
		Goto ReadyReal;

	//FuckingMother
	Template:
		IRND A 0;
		IFIN A 0;
		Goto Ready;
	}
}

Class RMD_AR70_Bullet : RMD_BaseBullet
{
	Default
	{		
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 16; 
		RMD_BaseBullet.DamageRand 1.4;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 240;		
		
		Scale 0.75;
		Decal "RMD_AR_BulletHoles";
	}
}



