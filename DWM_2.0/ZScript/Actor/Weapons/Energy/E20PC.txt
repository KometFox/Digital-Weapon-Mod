class RMD_E20PlasmaCannon : BaseWeapon
{
	double ScopeZoom;
	const AmmoConsum = 40;

	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_Cells";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIE20";
		Obituary "$OB_MPCHAINGUN";
		Tag "E20 Plasma Cannon";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 1;
		BaseWeapon.Magazine 1;
		
	}
	
	Action void MuzzleFlash(Vector3 POffset)
	{
		Vector3 x, y, z;
		int Ydir = -1;
		[x, y, z] = dt_Matrix4.GetAxes(pitch,angle,roll);
		Vector3 origin = Vec2OffsetZ(0,0,player.viewz)+10.0*x+3.0*y-1.8*z;	
	}
	
	Action void FirePlasma()
	{
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			A_FireProjectile("E20PC_PlasmaBall", 0, 1, 6, 4, CMF_AIMDIRECTION, 0);
			B_MuzzleFlash("RMD_HeavyPlasmaFlash", (15, 6.0, -2.0));	
		}
		else		
		{
			A_FireProjectile("E20PC_PlasmaBall", 0, 1, 0, 4, CMF_AIMDIRECTION, 0);
		}
		
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);	
		A_StartSound("E20PC/Fire", CHAN_WEAPON);
		A_AlertMonsters();
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOCELL";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		invoker.ScopeZoom = 1.0;
		
		InitWeapon(CIWpn);	
	}
	
	Action void ZoomIn()
	{
		if ( Invoker.ScopeZoom <= 8.0 )
		{
			A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
			Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
		}
	}
	
	Action void ZoomOut()
	{
		A_SetCrosshair(0);
		A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
	}
	
	States
	{

	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB );
		Loop;	

	Deselect:
		SELC DCBA 1 A_Lower;
		SELC A 1 A_Lower;
		TNT1 A 0 A_Lower(100);
		Loop;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SELC ABCD 1 A_Raise;
		IDLE A 1 A_Raise(100);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronReady:
		SCOA A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
		
	IronSightDeselect:
		IDLE A 0 ZoomOut();
		IDLE A 7;
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto ReadyReal;
		
	AltHold:
		SCOA A 1 ZoomIn();
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine + AmmoConsum - 1, "ReadyReal");
		FIR1 A 2 FirePlasma();
		FIR1 BCDEFGHIJKL 2;
		FIR1 A 0 A_Refire("FireNow");
		Goto ReadyReal;

	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine + AmmoConsum - 1, "IronReady");
		SCOA A 2 FirePlasma(); 
		SCOA AAAAAAAAAAA 2;
		SCOA A 0 A_Refire("IronFireNow");
		Goto IronReady;
	
	Reload:
		TNT1 A 0 ReloadCheck(AmmoConsum);
		Goto ReadyReal;

	ReloadNow:
		REL1 ABCDEFGHIJKLMNOPQRSTU 2;
		TNT1 A 0
		{
			A_StartSound("E20PC/CellOut", CHAN_WEAPON);
		}
		REL1 UVWXYZ 2;
		REL2 AB 2;
		REL2 C 20;
		TNT1 A 0
		{
			A_StartSound("E20PC/CellIn", CHAN_WEAPON +1);
			ReloadGun(Invoker.AmmoType1, 0, 1, AmmoConsum);
		}		
		REL2 DEFGHIJKLMN 2;
		Goto ReadyReal;

	}
}


Class E20PC_PlasmaBall : HeavyExplosivesFast
{
	const FLARE = SXF_ABSOLUTEVELOCITY + SXF_ABSOLUTEANGLE + SXF_TRANSFERPITCH;

	Default
	{
		Speed 75;
		Scale 2.5;
		Decal "RMD_LargeExplosion";
		MissileType "RMD_BigPlasma_Trail";
		
		//Impact 
		BaseProjectileFast.ImpactDamage 0;
		//Splash 
		
		
		BaseProjectileFast.SplashDamage 2000;
		BaseProjectileFast.FullSplashDamage 160;
		BaseProjectileFast.SplashRadius 240;
		//Randomizer
		BaseProjectileFast.NerfDamageRandom 1.0;
		BaseProjectileFast.NormalDamageRandom 1.0;
		
		DamageType "Plasma";
		DeathType "Plasma";
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				Actor Flare;
				
				Flare = Spawn("RMD_BigPlasmaTrail_Flare", Pos, ALLOW_REPLACE);
						
				If (Flare)
				{
					Flare.Target = Self;
					Flare.Scale = (0.37, 0.37);
					Flare.SetShade("Steel Blue");	
				}			
			}
			Goto SpawnLoop;
		SpawnLoop:	
			IDLE A 1;
			Loop;
		Explode:
			TNT1 A 0 
			{
				A_StartSound("E20PC/Explode");
				A_SpawnItemEx("RMD_BigPlasmaSpawner", 0, 0, 0);
			}

			Stop;	
	}
}
