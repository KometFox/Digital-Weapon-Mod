class RMD_E12PR : BaseWeapon
{
	double ScopeZoom;

	Enum E_FireMode
	{
		BULLET,
		GRENADE,
	}

	Default
	{
		Weapon.SlotNumber 5;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType1 "RMD_Cells";
		Weapon.AmmoType2 "RMD_Cells";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIE12PR";
		Obituary "$OB_MPCHAINGUN";
		Tag "E12 Plasma Rifle";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		BaseWeapon.FireMode BULLET;
		
		BaseWeapon.MagazineMax 50;
		BaseWeapon.Magazine 50;	
	}
	
	//Blam blam
	Action Void FireBullets()
	{
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
			

			
		if (Invoker.IronSight == IRON_INACTIVE)
		{	
			A_FireProjectile("RMD_E12PR_Plasma",frandom(-0.75, 0.75), 1, 6, 4, CMF_AIMDIRECTION,frandom(-0.75, 0.75));
			
			B_MuzzleFlash("RMD_PlasmaFlash", (10, 2.0, -0.7));	
		}
		else if (Invoker.IronSight == IRON_ACTIVE)
		{
			A_FireProjectile("RMD_E12PR_Plasma",frandom(-0.15, 0.15), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.15, 0.15));
			
		}

		A_AlertMonsters();
		A_StartSound("E12PR/Fire", CHAN_WEAPON);
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOCELL";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		invoker.ScopeZoom = 1.0;
		
		InitWeapon(CIWpn);	
	}
	
	Action void ZoomIn()
	{
		if ( Invoker.ScopeZoom <= 8.0 )
		{
			A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
			Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
		}
	}
	
	Action void ZoomOut()
	{
		A_SetCrosshair(0);
		A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
	}

	States
	{

	Init:
		IDLE A 0 B_InitWeapon();
		Goto ReadyReal;
	
	ReadyReal:
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	//Ironsight Mode
	IronReady:
		SCOA C 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWUSER1);
		Loop;
		
	Deselect:
		SEL1 EDCBA 1 ;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Wait;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SEL1 ABCDE 1;
		SEL1 A 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronSightDeselect:
		IDLE A 0 ZoomOut();
		IDLE A 7;
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto ReadyReal;
		
	AltHold:
		SCOA C 1 ZoomIn();
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;
	
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;

	//Bullets
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 1 FireBullets();
		FIR1 BCD 1;
		FIR1 A 0 A_ReFire("FireNow");
		FIR1 EF 1;
		Goto ReadyReal;
	
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		SCOA C 1 FireBullets();
		SCOA CCC 1;
		SCOA C 0 A_ReFire("IronFireNow");
		SCOA CC 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;
		
	//The Magazine
	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 99);
		REL1 ABCDEFG 2;
		TNT1 A 0
		{
			A_StartSound("M16A3/MagOut", CHAN_WEAPON +3);
			A_StartSound("E12PR/CellOut", CHAN_WEAPON +1);
			A_FireProjectile("EmptyMagSpawn",-5,0,8,-4);
		}
		REL1 HIJKLMN 2;
		TNT1 A 0
		{
			A_StartSound("M16A3/MagIn", CHAN_WEAPON +4);
			A_StartSound("E12PR/CellIn", CHAN_WEAPON +2);
			
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
		}
		REL1 OP 2;		
		Goto ReadyReal;

	
	//FuckingMother
	Template:
		IRND A 0;
		IFIN A 0;
		Goto Ready;
	}
}

Class RMD_E12PR_Plasma : RMD_BaseBullet
{
	Default
	{		
		Speed 200;
	
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 16; 
		RMD_BaseBullet.DamageRand 1.7;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 1000;		
		RMD_BaseBullet.ExtremeDeath True;
		
		Scale 0.75;
		Decal "RMD_AR_BulletHoles";
		DamageType "Plasma";
		DeathType "Plasma";
	}
	
	States
	{
	
		Spawn:
			TNT1 A 0;
			TNT1 A 0
			{
				Actor Flare;
				
				Flare = Spawn("RMD_BasebulletFlare", Pos, ALLOW_REPLACE);
						
				If (Flare)
				{
					Flare.Target = Self;
					Flare.A_SetRenderStyle(1.0, STYLE_AddShaded);
					Flare.SetShade("00a4ff");
				}			
			}
			Goto Spawn1;
			
			
		Death:
			TNT1 A 0;
			Goto Explode;
		Crash:
			TNT1 A 0;
			Goto Explode;
		XDeath:
			TNT1 A 0;
			Goto Explode;
			
		Explode:
			TNT1 A 0 
			{
				A_SpawnItemEx("RMD_BulletPlasmaImpact", 0, 0, 0);
				A_StartSound("PlasmaBoom");
				
				int GiveRadius = 60;

				BlockThingsIterator it = BlockThingsIterator.Create(self, GiveRadius);
				
				while (it.Next())
				{
					let obj = it.thing;
				
					if ((obj.bISMONSTER) && (!target || obj != target) && obj.health > 0 && Distance3D(obj) <= GiveRadius)
					{
						RMD_FireInv.AttachFire(Obj, Self, "RMD_PlasmaDeath", "PlasmaBurn");
					}
				}
			}
			Stop;	
	}
	
}



