class RMD_Enforcer : BaseWeapon
{

	Default
	{
		Weapon.SlotNumber 2;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_PistolBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIENF";
		Obituary "$OB_MPCHAINGUN";
		Tag "Enforcer";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 14;
		BaseWeapon.Magazine 14;	
		+NoExtremeDeath;
	}
	
	action state ReloadCheck()
	{
		return B_ReloadGoTo(
								"ReadyReal", 
								"IronReady", 
								"IronSightDeselect", 
								"ReloadNow", 
								Invoker.AmmoType1,
								Invoker.Magazine,
								Invoker.MagazineMax,
								1);
	}
	

	Action void FireBullets()
	{
		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			//A_FireProjectile("RMD_EnforcerBullet",frandom(-0.5, 0.5), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.4, 0.4));
			A_FireBullets(0.5, 0.5, -1, 45, "RMD_BulletImpact", FBF_NoRandom); 
		
			//Effect
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (12, 1.5, -0.5));	
			B_SpawnCasing("PistolCase", (8, -3.0, -1.5));
		}

		else
		{
			//A_FireProjectile("RMD_EnforcerBullet",frandom(-0.2, 0.2), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.14, 0.14));
			A_FireBullets(0.2, 0.2, -1, 45, "RMD_BulletImpact", FBF_NoRandom); 

			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (12, 0, -1.0));	
			B_SpawnCasing("PistolCase", (7, 0, -2.0));
		}	

		A_StartSound("Enforcer/Fire", CHAN_WEAPON);
		A_AlertMonsters();
		
		B_WpnRecoil(0.75);
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOPIST";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}
	

	States
	{
	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	

	Deselect:
		SELC FEDCBA 1;
		SELC A 1 A_Lower(int.max);
		Wait;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SELC ABCDEF 1;
		SELC F 1 A_Raise(100);
        Wait;
			
	//Ironsight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_NOBOB);
		Loop;
		
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(2.15);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto ReadyReal;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 3 FireBullets();
		FIR1 BCD 2;
		FIR1 EF 2;
		FIR1 F 0 A_ReFire();
		Goto ReadyReal;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 3 FireBullets();
		IFI1 BCD 2;
		IFI1 EF 2;
		IFI1 F 0 A_ReFire;
		Goto IronReady;	
	
	Reload:
		TNT1 A 0 ReloadCheck();
		Goto ReadyReal;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		REL1 AB 2;
		TNT1 A 0
		{
			A_StartSound("Enforcer/MagOut", CHAN_WEAPON);
			A_FireProjectile("PistolMagSpawn",-5,0,8,-4);
		}
		REL1 CDEFG 2;
		TNT1 A 0
		{
			A_StartSound("Enforcer/MagIn", CHAN_WEAPON +1);
			ReloadGun(Invoker.AmmoType1, 0, 1, 1);
		}
		REL1 HIJK 2;		
		Goto ReadyReal;
		
	}
}


Class RMD_EnforcerBullet : RMD_BaseBullet
{

Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 33; 
		RMD_BaseBullet.DamageRand 1.6;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 500;
		
		Scale 0.75;
		Decal "RMD_Pistol_BulletHoles";
	}
}