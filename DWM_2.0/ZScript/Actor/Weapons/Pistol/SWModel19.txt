class RMD_SWModel19 : BaseWeapon
{
	const AmmoConsum = 2;

	Default
	{
		Weapon.SlotNumber 2;
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_PistolBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PISWM19";
		Obituary "$OB_MPCHAINGUN";
		Tag "Smith & Wesson Model 19";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 6;
		BaseWeapon.Magazine 6;	
		+NoExtremeDeath;
	}

	Action void FireBullets()
	{
		Invoker.Magazine -= 1;
		Invoker.Magazine = Clamp(Invoker.Magazine, 0, 6);
		
		RMD_BARINFO.Set_Mag(invoker.Magazine);
		
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			//A_FireProjectile("RMD_Model19Bullet",frandom(-0.7, 0.7), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.5, 0.5));
			A_FireBullets(0.7, 0.7, -1, 100, "RMD_BulletImpact", FBF_NoRandom); 

			
			//Effect
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (15, 4.5, -0.5));	
		}

		else
		{
			//A_FireProjectile("RMD_Model19Bullet",frandom(-0.3, 0.3), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.2, 0.2));
			A_FireBullets(0.3, 0.3, -1, 100, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (12, 0, -1.0));	
		}	

		A_StartSound("SWModel19/Fire", CHAN_WEAPON);
		A_AlertMonsters();
	}
	
	Action state FireCheck()
	{
		if (Invoker.IronSight == IRON_ACTIVE)
			return ResolveState("IronFireNow");	
		else
			return ResolveState("FireNow");		
	}

	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOPIST";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;
		
		InitWeapon(CIWpn);	
	}
	

	States
	{

	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		CHGG A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;	

	Deselect:
		SELC FEDCBA 1;
		SELC A 1 A_Lower(int.max);
		Wait;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SELC ABCDEF 1;
		SELC F 1 A_Raise(100);
        Wait;
			
	//Ironsight Mode
	IronReady:
		IRO1 D 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
		
	IronSightAnim:
		TNT1 A 0 A_SetCrosshair(99);
		IRO1 ABCD 2 A_ZoomFactor(2.15);
		Goto IronReady;
		
	IronSightDeselect:
		TNT1 A 0  A_SetCrosshair(0);
		IRO1 DCBA 2 A_ZoomFactor(1.0);
		TNT1 A 0
		{
			Invoker.IronSight = IRON_INACTIVE;
			
			if (CountInv("GotoReload") > 0)
			{
				Return A_Jump(256, "ReloadNow");
			}

			Return A_Jump(256, "ReadyReal");			
		}
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("IronSightAnim", "IronSightDeselect");
		Goto ReadyReal;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 7 FireBullets();
		FIR1 BCD 2;
		FIR1 E 2;
		FIR1 E 0 A_ReFire();
		Goto ReadyReal;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		IFI1 A 7 FireBullets();
		IFI1 BCD 2;
		IFI1 E 2;
		IFI1 E 0 A_ReFire;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck(3);
		Goto ReadyReal;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		REL1 ABCD 2;
		TNT1 A 0
		{
			A_StartSound("SWModel19/Open", CHAN_WEAPON);
			
			for (int i = 0; i < 6; i++)
			{
				A_FireProjectile("PistolCase",-5,0,8,-4);
			}
		}
		REL1 EFGHI 2;
		TNT1 A 0
		{			
			A_StartSound("SWModel19/Close", CHAN_WEAPON);
			ReloadGun(Invoker.AmmoType1, 0, 1, AmmoConsum);
		}
		REL1 J 2;		
		Goto ReadyReal;
		
	}
}


Class RMD_Model19Bullet : RMD_BaseBullet
{

Default
	{
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 90; 
		RMD_BaseBullet.DamageRand 1.4;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 500;
		
		Scale 0.75;
		Decal "RMD_Pistol_BulletHoles";
	}
}