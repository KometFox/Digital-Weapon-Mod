class RMD_M249SAW : BaseWeapon
{
	double ScopeZoom; 
	const AmmoConsum = 1; 

	Default
	{
		Weapon.SelectionOrder 10;
		Weapon.SlotNumber 5;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_MediumBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PIM249SW";
		Obituary "$OB_MPCHAINGUN";
		Tag "M249 SAW";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 200;
		BaseWeapon.Magazine 200; 
	}
	
	Action void FireBullets()
	{
		if (Invoker.IronSight == IRON_INACTIVE)
		{
			//A_FireProjectile("RMD_M249SAW_Bullet",frandom(-0.7, 0.7), 1, 6, 4, CMF_AIMDIRECTION,frandom(-0.7, 0.7));
			A_FireBullets(0.7, 0.7, -1, 25, "RMD_BulletImpact", FBF_NoRandom); 
			
			B_MuzzleFlash("RMD_BulletMuzzleFlash", (15, 2.6, -1.5));	
			C_SpawnCasing("RifleCase", (6, 3.0, -1.0), 90);
		}
		else
		{
			//A_FireProjectile("RMD_M249SAW_Bullet",frandom(-0.3, 0.3), 1, 0, 4, CMF_AIMDIRECTION,frandom(-0.3, 0.3));
			A_FireBullets(0.2, 0.2, -1, 25, "RMD_BulletImpact", FBF_NoRandom); 
			
			C_SpawnCasing("RifleCase", (12, 15.0, -0.5), 90);
		}


		Invoker.Magazine -= 1;
		RMD_BARINFO.Set_Mag(invoker.Magazine);
			
		A_AlertMonsters();
		A_StartSound("M249SAW/Fire", CHAN_WEAPON);
		B_WpnRecoil(0.45);
	}

	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOAR";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;

		InitWeapon(CIWpn);	
	}
	
	Action void ZoomIn()
	{
		if ( Invoker.ScopeZoom <= 8.0 )
		{
			A_ZoomFactor(Invoker.ScopeZoom *= 1.1);
			Invoker.ScopeZoom = min(Invoker.ScopeZoom, 8.1);
		}
	}
	
	Action void ZoomOut()
	{
		A_SetCrosshair(0);
		A_ZoomFactor(invoker.ScopeZoom = 1.0, ZOOM_INSTANT);
	}
	
	States
	{
		

	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		IDLE A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;

	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		SEL1 JIHGFEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Wait;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SEL1 ABCDEFGHIJ 1;
		SEL1 J 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
			
	//Ironsight Mode
	IronReady:
		SCOA B 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;
		
	IronSightAnim:
		IRO1 A 0;
		IRO1 ABCD 2;
		Goto IronReady;
		
	//Ironsight Mode
	IronSightDeselect:
		IDLE A 0 ZoomOut();
		IDLE A 7;
		Goto ReadyReal;
		
	AltFire:
		TNT1 A 0 B_GoToIronSight("AltHold", "IronSightDeselect");
		Goto Ready;
		
	AltHold:
		SCOA B 1 ZoomIn();
		TNT1 A 0 A_Refire("AltHold");
		Goto IronReady;
		
	Fire:
		TNT1 A 0 FireCheck();
		Goto ReadyReal;
		
	FireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "ReadyReal");
		FIR1 A 2 FireBullets();
		FIR1 B 1;
		IDLE A 0 A_ReFire("FireNow");
		FIR1 CEFGH 1;
		Goto ReadyReal;
		
	IronFireNow:
		TNT1 A 0 CheckIfAmmo(Invoker.Magazine, "IronReady");
		SCOA B 2 FireBullets();
		SCOA B 1;
		SCOA B 0 A_ReFire("IronFireNow");
		SCOA BBBBB 1;
		Goto IronReady;	
		
	Reload:
		TNT1 A 0 ReloadCheck(1);
		Goto ReadyReal;

	ReloadNow:
		TNT1 A 0 A_TakeInventory("GotoReload", 1);
		//Cock
		REL1 ABCDEFGHIJKLMNOPQR 2;
		TNT1 A 0 A_StartSound("M249SAW/Bolt", CHAN_WEAPON);
		//Hatch Open
		REL1 STUVWXYZ 2;
		REL2 ABCD 2; 
		TNT1 A 0 A_StartSound("M249SAW/HatchOpen", CHAN_WEAPON);
		//Belt Touch;
		REL2 DEFGHIJKLM 2;
		TNT1 A 0 A_StartSound("M249SAW/Belt", CHAN_WEAPON +1);
		//Mag Out
		REL2 NOPQRS 2;
		TNT1 A 0
		{
			A_FireProjectile("MGMagSpawn",-5,0,8,-4);
			A_StartSound("M249SAW/MagOut", CHAN_WEAPON +2);
		}
		//Mag In
		REL2 TUVWXYZ 2;
		REL3 ABC 2;
		TNT1 A 0
		{
			A_StartSound("M249SAW/MagIn", CHAN_WEAPON +2);
			
			ReloadGun(Invoker.AmmoType1, 0, 1, AmmoConsum);
		}
		//Hatch Close
		REL3 DEFGHIJK 2;
		REL3 L 2 A_StartSound("M249SAW/HatchClose", CHAN_WEAPON);
		REL3 MNOPQRS 2;
		Goto ReadyReal;		
	}
}


Class RMD_M249SAW_Bullet : RMD_BaseBullet
{

Default
	{		
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 16; 
		RMD_BaseBullet.DamageRand 1.5;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 450;		
		
		Scale 0.75;
		Decal "RMD_SMG_BulletHoles";
	}
}