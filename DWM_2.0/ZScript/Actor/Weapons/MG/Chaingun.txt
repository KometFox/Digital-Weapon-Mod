class RMD_Chaingun : BaseWeapon
{
	double ScopeZoom; 
	Const AmmoConsume = 1;

	Default
	{
		Weapon.SelectionOrder 10;
		Weapon.SlotNumber 5;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "RMD_MediumBullets";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.AltHudIcon "PICHAIN";
		Obituary "$OB_MPCHAINGUN";
		Tag "Chaingun";
		
		BaseWeapon.IronSight IRON_INACTIVE;
		
		BaseWeapon.MagazineMax 0;
		BaseWeapon.Magazine 0; 
	}
	
	action void C_SpawnCasing(Class<Actor> Obj = "PistolCase", Vector3 COffset = (0, 0, 0), int PAngle = 0)
	{
		Vector3 x, y, z;
		int Ydir = 1;
		[x, y, z] = dt_Matrix4.GetAxes(pitch, angle, roll);
		Vector3 origin = Vec2OffsetZ(0,0,player.viewz) + 10.0 * x + 3.0 * y - 1.8  * z;

	
		origin += x * (1.0 + COffset.x) + ydir * y * (6.0 + COffset.y) -z * (2.0 + COffset.z);		
		let C = Spawn(Obj, origin);
		C.vel = x*FRandom[Junk](-0.5, 0.5) + y * ydir * FRandom[Junk](1.5,3) + z * FRandom[Junk](1.5,2);	
	}	
	
	Action void FireBullets()
	{
		//A_FireProjectile("RMD_Chaingun_Bullet",frandom(-1.6, 1.6), 1, 6, 4, CMF_AIMDIRECTION,frandom(-1.6, 1.6));
		A_FireBullets(1.4, 1.4, -1, 30, "RMD_BulletImpact", FBF_NoRandom); 
			
			
		B_MuzzleFlash("RMD_BulletMuzzleFlash", (15, 3.2, -3.0));	
		C_SpawnCasing("RifleCase", (6, 3.0, -1.0), 90);

		A_AlertMonsters();
		A_StartSound("Chaingun/Fire", CHAN_WEAPON);
		A_TakeInventory("RMD_MediumBullets", AmmoConsume);
	}
	
	Action void B_InitWeapon()
	{
		RMD_InitWeapon CIWpn = new("RMD_InitWeapon");
		
		CIWpn.AmmoIcon = "AMMOAR";
		CIWpn.Ammo1 = Invoker.AmmoType1.GetClassName();
		CIWpn.Magazine = Invoker.Magazine;

		InitWeapon(CIWpn);	
	}
	
	States
	{
		

	Init:
		TNT1 A 0 B_InitWeapon();
		Goto ReadyReal;
		
	ReadyReal:
		IDLE A 0;
		IDLE A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB);
		Loop;

	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		SEL1 JIHGFEDCBA 1;
		SEL1 A 1 A_Lower(int.max);
		TNT1 A 0 A_Lower(int.max);
		Wait;
		
	Select:
		TNT1 A 0 B_InitWeapon();
		SEL1 ABCDEFGHIJ 1;
		SEL1 J 1 A_Raise(int.max);
		IDLE A 1 A_Raise(int.max);
        Goto ReadyReal;
			
	AltFire:
		TNT1 A 0;
		Goto Ready;
		
	Fire:
		TNT1 A 0
		{
			if (Player.mo.CountInv("RMD_MediumBullets") < AmmoConsume)
				return ResolveState("Ready");
				
			return A_Jump(0, "Ready");
		}
		Goto FireNow;
		
	FireNow:
		FIR1 A 2 FireBullets();
		FIR1 B 1;
		FIR1 C 1;
		FIR1 A 0 A_Refire();
		FIR1 DE 2;
		Goto ReadyReal;	
	}
}


Class RMD_Chaingun_Bullet : RMD_BaseBullet
{

Default
	{		
		//Normal Damage
		RMD_BaseBullet.ImpactDamage 33;
		RMD_BaseBullet.DamageRand 1.45;
		//Nerfed Damage
		RMD_BaseBullet.HealthDamage 450;		
		Speed 110;
		
		Scale 0.75;
		Decal "RMD_SMG_BulletHoles";
	}
}