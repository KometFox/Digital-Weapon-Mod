/*
The Wisp, none can clearly explain how it got created, the folklore says
it appears mainly in certain types of swamp but since the fall most witness
reports that the wisp appearance is much more common. Few unfortante victims
were blinded by its deceitful beautiful appearnce as soon they approached close 
to it they suddendly felt a strong pain of electricity around their body. Soon 
after that they dropped death like a sack and it smells like a strong smell of
burnt flesh. Scientist are not sure how it is kept alive, the only speculation
is that is held by pure Psi power.

*/

ACTOR Wisp
{
  Health 40
  Radius 16
  Height 38
  Mass 500
  Speed 9
  Damage 0
  Scale 0.12
  PainChance 256
  Monster
  Species "Wisp"
  +FLOAT
  +NOGRAVITY
  +MISSILEMORE
  +DONTFALL
  +NOICEDEATH
  +NOBLOOD
  +DONTHARMSPECIES
  +DONTHARMCLASS
  AttackSound "Wisp/Idle"
  PainSound "Wisp/Pain"
  DeathSound "Wisp/Pain"
  ActiveSound "Wisp/Idle"
  RenderStyle Add
  Obituary "%o got charred from wisp deadly aura."
  DamageType M_Aura
  DamageFactor Fire, 2
  States
  {
  Spawn:
    LENB AA 10 Bright A_Look
    Loop
  See:
    LENB AAAA 2 Bright A_Chase("","")
	LENB A 0 Bright A_CustomMissile("Wisp_Aura",0,0,0)
	LENB AA 0 A_SpawnItemEx("Wisp_Trail",0,0,0,+frandom(-1,1),+frandom(-1,1),+frandom(-1,3))
	TNT1 A 0 A_Jump(64,"Wander")
    Loop
  Wander:
	LENB A 0 A_SpawnItemEx("Wisp_Trail",0,0,0,+frandom(-1,1),+frandom(-1,1),+frandom(-1,3))
    LENB AA 8 Bright A_Wander
	LENB A 1 Bright A_CustomMissile("Wisp_Aura",0,0,0)
	LENB AA 0 A_SpawnItemEx("Wisp_Trail",0,0,0,+frandom(-1,1),+frandom(-1,1),+frandom(-1,3))
    Goto See  
  Missile:
    LENB AAA 1 Bright A_FaceTarget
    LENB A 4 Bright A_SkullAttack
	LENB AA 0 A_SpawnItemEx("Wisp_Trail",0,0,0,+frandom(-1,1),+frandom(-1,1),+frandom(-1,3))
    Goto Missile+2
  Pain:
    LENB A 1 Bright A_CustomMissile("Wisp_Aura",0,0,0)
	LENB AA 0 A_SpawnItemEx("Wisp_Trail",0,0,0,+frandom(-1,1),+frandom(-1,1),+frandom(-1,3))
    Goto See
  Death:
	TNT1 A 0 A_NoBlocking
    LENB A 1 Bright A_Scream
	LENB AAAAAA 0 A_SpawnItemEx("Wisp_Trail",0,0,0,+frandom(-5,-5),+frandom(-5,-5),+frandom(-5,8))
	LENB AAAAAAAAA 1 A_FadeOut
    Stop
  }
}

ACTOR Wisp_Trail : SparkParticles
{
   -NOGRAVITY
   +MISSILE
   +BRIGHT
    Gravity 0.02
	Scale 0.08
	BounceCount 1
	States
	{
	Spawn:
		TNT1 A 0
		Goto Death
	Death:
	    PLAS HIJHIJHIJHIJ 2
		PLAS HHIIKJ 1 A_FadeOut(0.3)  
		Stop		
	}
}


ACTOR Wisp_Aura
{
   -NOGRAVITY
   +MISSILE
   +BRIGHT
    Species "Wisp"
	DamageType M_Aura
	Scale 0.2
	Alpha 0.8 
	Renderstyle Add
	States
	{
	Spawn:
		TNT1 A 0
	Death:
	    LENB A 1
		TNT1 A 0 A_Explode(6,140,0,0,140)
		Stop		
	}
}
