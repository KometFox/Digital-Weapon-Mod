/*
Man O' War, a hideous creature whose floating abilitys can only be explained
by its Psi abilities it uses also said abilitys to attack its victim and
to fry the victims brain even if it got a armored helmet, a specialized
Psi helmet is needed to reduce its effect. Man O' War resides commonly in 
biomass infected areas.
*/

ACTOR ManOWar
{
  Health 270
  Radius 31
  Height 56
  Mass 400
  Speed 8
  PainChance 128
  Monster
  +FLOAT
  +NOGRAVITY
  SeeSound "Mutants/Growl"
  AttackSound "Mutants/Attack"
  PainSound "Mutants/Pain"
  DeathSound "Mutants/Die"
  ActiveSound "Mutants/Growl"
  HitObituary "%o got a close combat shock therapy from a Man O' War."
  Obituary "%o brain got toasted by a Man O' War." 
  Tag "Man O' War"
  DamageFactor M_Aura, 0
  States
  {
  Spawn:
    HEAD A 10 A_Look
    Loop
  See:
    HEAD A 3 A_Chase
    Loop
  Missile:
    HEAD BC 5 A_FaceTarget
    HEAD D 5 Bright 
	HEAD DDDDDDD 3 Bright  A_CustomMissile("ManOWar_Psi", 30,0,+frandom(-0.5,0.5),CMF_OFFSETPITCH,+frandom(-0.5,0.5))
    Goto See
  Melee:
    HEAD BC 2 A_FaceTarget
    HEAD D 5 Bright 
	HEAD DDDD 0 Bright  A_CustomMissile("ManOWar_Psi", 30,0,+frandom(-12,12),CMF_OFFSETPITCH,+frandom(-12,12))
	HEAD C 30 Bright
    Goto See
  Pain:
    HEAD E 3
    HEAD E 3 A_Pain
    HEAD F 6
    Goto See
  Death:
	TNT1 A 0 A_Jump(15,"PsiLaugh")
	Goto RealDeath
  PsiLaugh:
	TNT1 A 0 A_CustomMissile("ManoWar_PsiEffect",0,0,0)
	Goto RealDeath
  RealDeath:
    HEAD G 8
    HEAD H 8 A_Scream
    HEAD IJ 8
    HEAD K 8 A_NoBlocking
    HEAD L -1 A_SetFloorClip
    Stop
  
	
  Raise:
    HEAD L 8 A_UnSetFloorClip
    HEAD KJIHG 8
    Goto See
  }
}

ACTOR ManoWar_PsiEffect
{
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 1
	TNT1 A 5 A_PlaySound("PSI/Laughter", 0, 0.25, 0, ATTN_NONE)
	Stop
	}
}

ACTOR ManOWar_Psi
{
  Radius 8
  Height 8
  Speed 32
  Damage 10
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 1
  SeeSound "Psi/Attack"
  DeathSound ""
  Damagetype Psi
  States
  {
  Spawn:
	TNT1 A 0 A_SpawnItemEx("ManOWar_PsiTracer",0,0,0)
    BAL2 AB 1 Bright A_SpawnItemEx("ManOWar_PsiTracer",0,0,0)
    Loop
  Death:
    BAL2 CDE 6 Bright
   Stop
  }
}

ACTOR ManOWar_PsiTracer : SparkParticles
{
	Speed 0
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
    Gravity 0.01
	Scale 1.6
	BounceCount 0
	Renderstyle Add
	Alpha 0.4
	States
	{
	Spawn:
	    BAL2 ABABABABAB 2 Bright A_SetScale(ScaleX * 0.8, ScaleY * 0.8)
		Stop		
	}
}